Class BD_Arachnotron : BDoomMonster {
	Default	{
		species "Arachnotron";
		Health 500;
		Radius 64;
		Height 64;
		Mass 600;
		Speed 12;
		PainChance 128;
		Monster;
		+FLOORCLIP
		+BOSSDEATH
		SeeSound "baby/sight";
		PainSound "baby/pain";
		DeathSound "baby/death";
		ActiveSound "baby/active";
		Obituary "$OB_BABY";
		Tag "$FN_ARACH";
	}
	states	{
	Death.Fire:
	Death.GreenFire:
		goto Death.PlasmaDamage;
	Spawn:
		ARAI AB 10 A_Look();
		loop;
	Crush:
		CRS2 A -1;
		stop;
	see:
		ARAC a 20;
		ARAC a 3 a_babymetal();
		ARAC bcdee 3 a_chase();
		ARAC f 3 a_babymetal();
		ARAC ghijj 3 a_chase();
		goto see+1;
	missile:
		ARAC a 20 a_facetarget();
		ARAT a 3 {
			A_FaceTarget();
			if (bdoom_weapons == 2)
				A_Spawnprojectile("ModernArachnotronPlasma");
			else 
				A_Spawnprojectile("BD_ArachnotronPlasma");
		}
		ARAT b 3;
		ARAT c 3 a_SpidRefire();
		goto missile+1;
	pain:
		ARPA a 3;
		ARPA b 3 a_pain();
		goto see+1;
	Death:
		ARAM a 3 { scale.x = randompick[bdsfx](-1,1); }
		ARAM b 3 a_scream();
		ARAM c 3 a_noblocking();
		ARAM defghI 3 {
			if (bdoom_gibs && !burning)
				A_SpawnItemEx("BasicMeatPiece",frandom(-12,-18),frandom(-8,-8),frandom(32,48),	frandom(2,5),0,frandom(3,4),random(70,100)*scale.x,SXF_SETTARGET,40);
		}
		ARAM jklm 3 BD_Bleed();
		goto deathbleed;
	XDeath:
		stop;
}
}

Class BD_ArachnotronPlasma : BD_Plasmaball {
	Default	{
		BD_Plasmaball.pcolor "green";
		decal "ArachnotronPlasmaSplat";
		translation "GreenPlasma";
	}
}

Class ModernArachnotronPlasma : BD_Plasmaball {
	Default	{
		BD_Plasmaball.pcolor "yellow";
		decal "YellowPlasmaSplat";
		speed 30;
		SeeSound "Mweapons/plasma/fire";
		translation "192:207=208:223";
	}
	States	{
	Spawn:
		ARMP ABCDEFGHIJKLMNO 1 bright;
		loop;
	Death:
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
			if (bdoom_debris) {
				A_SpawnItem("WeaponSmoke");
				for (int i = 12; i > 0; i --) {
					A_spawnItemEx("BluePlasmaPiece",0,0,1,frandom(-3,3),frandom(-3,3),frandom(3,6),flags:SXF_TRANSFERTRANSLATION,failchance:48);
			}
		}
	}
		TNT1 A 0 A_SetScale(0.2*randompick(-1,1),0.2*randompick(-1,1));
		PEXP ABCDEFGHIJKLM 2 bright A_FadeOut(0.05);
		stop;
	}
}

	
