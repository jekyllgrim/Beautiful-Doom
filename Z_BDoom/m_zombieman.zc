Class BD_ZombieMan : BDoomMonster {
	Default {
		species "ZombieMan";
		+DOHARMSPECIES
		Health 20;
		radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		SeeSound "grunt/sight";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		Obituary "$OB_ZOMBIE";
		Tag "$FN_ZOMBIE";
		BdoomMonster.monsterclip 15;
		dropitem "BD_Pistol",40,1;
		dropitem "Clip",256;
	}
	states {
	Spawn:
		PSSS AB 10 A_Look();
		loop;
	See:
		TNT1 A 0 {
			A_SetScale(default.scale.x,default.scale.y);
			bFRIGHTENED = false;
		}
		POS1 ABCDDCBA 4 A_Chase();
		loop; 	
	Missile:
		TNT1 A 0 A_Jumpif(bdoom_weapons==2,"MissileModern");
		PSSA A 5 A_FaceTarget();
		PSSA B 2 A_FaceTarget();
		POS1 E 3 A_FaceTarget();
		POS1 F 3 BD_BulletAttack(22.5, 0, 1, random(1,5)*3,"grunt/attack","weapons/pistol/fire","PistolCasing",7,-8,30,30,8);
		POS1 E 3;
		PSSA BA 4;
		PSSA A 2;
		goto See;
	MissileModern:
		PSSA A 5 A_FaceTarget();
		PSSA B 2 A_FaceTarget();
		POS1 E 3 A_FaceTarget();
		POS1 F 3 {
			if (bdoom_debris == 1)
				A_SpawnItemEx("PistolCasing",7,-8,30,		frandom(2,4),0,random(2,4),	random(-85,-95),SXF_SETTARGET);
			A_PlaySound("Mweapons/pistol/fire");
			A_CustomBulletAttack(19.5,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",30,8);
			monsterclip--;
		}
		POS1 E 14;
		TNT1 A 0 BD_Refire(72,"MissileModernEnd");
		goto MissileModern+3;
	MissileModernEnd:
		PSSA BA 4;
		PSSA A 2;
		goto See;
	Reload:
		TNT1 A 0 {
			bFRIGHTENED = true;
			A_PlaySound("Mweapons/pistol/clipout");
			monsterclip = 15;
		}
		POS1 ABCDDCBAABCDDCBA 4 A_Chase(null,null);
		TNT1 A 0 A_PlaySound("Mweapons/pistol/clipin");
		POS1 ABCDDCBA 4 A_Chase(null,null);
		TNT1 A 0 { bFRIGHTENED = false; }
		goto see;
	Pain:
		stro a 3;
		stro b 3 A_Pain();
		goto see;
	Death:
		TNT1 A 0 {
			if (hitheight > default.height*0.8) {
				if (bdoom_gibs && random(1,2) == 2 && !burning)
					return ResolveState("DeathHeadshot");
				return ResolveState("DeathHeadshot1");
			}
			else if (random(1,3) == 3)
				return ResolveState("Death1");
			return ResolveState(null);
		}
	Death.Vanilla:
		POSM A 3 {
			A_Scream();
			BD_Bleed();
		}
		POSM B 3 {
			A_NoBlocking();
			BD_Bleed();
		}
		POSM CDEFG 3;
		POSM H 3;
		POSM I 1;
		goto deathbleed;
	Death1: //fall forward
		POSO A 3 BD_Bleed();
		POSO B 3 A_Scream();
		POSO C 3 A_NoBlocking();
		POSO DDDMMD random(4,7) {
			if (!burning && random(0,100) < 40) {
				A_PlaySound("gibs/bloodspurt");
				A_SpawnItemEx("FrontalSpurt",2,frandom(-0.1,0.1),29.5,0,0,0,random(-87,93),SXF_SETTARGET);
			}
		}
		POSO EFGH 3;
		POSO I 3 {
			A_PlaySound("body/fall");
			A_NoBlocking();
		}
		POSO JK 4;
		POSO L 1;
		goto deathbleed;
		
	DeathHeadshot: //fall left/right
		TNT1 A 0 { 
			scale.x*=randompick(-1,1);
			A_Scream();
		}
		POSN ABC 2 {
			if(!burning && bdoom_blood)
				A_SpawnItemEx("Blood",random(-2,2),0,random(44,48),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
		}
		TNT1 A 0 A_NoBlocking();
		POSN DEF 3 {
			if(!burning && bdoom_blood)
				A_SpawnItemEx("Blood",random(-2,2),0,random(32,36),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
		}
		POSN GHI 3 {
			if(!burning && bdoom_blood)
				A_SpawnItemEx("Blood",random(-2,2),0,random(22,26),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
		}
		POSN KLM 3;
		POSN N 1;
		goto deathbleed;
	DeathHeadshot1: //loses arm and head
		POSD A 2 {
			A_SpawnItemEx("Gib_Head",0,0,48,	frandom(-3,-4),frandom(-1,-2),frandom(3,5),	0,SXF_SETTARGET);
			A_SpawnItemEx("Gib_Hand",0,14,24,	frandom(-2,2),frandom(3,4),frandom(2,3),	0,SXF_SETTARGET);
			return ResolveState(null);
		}
		POSD BC 2 BD_Bleed();
		TNT1 A 0 A_NoBlocking();
		POSD DE 2 BD_Bleed();
		POSD FGH 2;
		goto deathbleed;
	Death.Saw:
		TNT1 A 0 {
			scale.x*=randompick(-1,1);
		}
		POSY AB 6;		
		TNT1 A 0 A_NoBlocking();
		POSY CD 6;
		POSY EFGHIJ 3 {
			if(bdoom_blood) {
				A_FaceTarget();
				A_SpawnItemEx("Blood",	frandom(-4,4),frandom(0,6),frandom(28,36),frandom(2,4),0,frandom(4,6),	random(-80,-90)*scale.x,SXF_SETTARGET,80);
			}
		}
		POSY K 3 A_PlaySound("body/fall",CHAN_5);
		POSY L 4;
		POSY M 3 A_PlaySound("gibs/meatland",CHAN_6);
		POSY NOP 7;
		goto deathbleed;
	XDeath:
		TNT1 A 0 A_JumpIf(bdoom_gibs==true,"XDeath.Particles");
	XDeath.Vanilla:
		ZGIB ab 2;
		ZGIB cd 2 A_XScream();
		ZGIB e 2 A_NoBlocking();
		ZGIB f 2;
		ZGIB ghijklmnopq 2;
		goto deathbleed;
	}
}