////////////////////////////////////////////////////////////
////////////// ALL BASIC BEAUTIFUL DOOM OBJECTS ////////////
////////////////////////////////////////////////////////////

Class BDoomActor : Actor abstract {
	protected double pi;		
	bool CheckLandingSize (double cradius = 0, bool checkceiling = false) {
		bool ret = false;
		if (checkceiling) {
			double ceilingHeight = GetZAt (flags: GZF_CEILING);
			for (int i = 0; i < 360; i += 45) {
				double curHeight = GetZAt (cradius, 0, i, GZF_ABSOLUTEANG | GZF_CEILING);
				ret = ret || (curHeight > ceilingHeight);
			}
		}
		else {
			double floorHeight = GetZAt ();
			for (int i = 0; i < 360; i += 45) {
				double curHeight = GetZAt (cradius, 0, i, GZF_ABSOLUTEANG);
				ret = ret || (curHeight < floorHeight);
			}
		}
		return ret;
	}
	int BD_Sign (int i) {
		if (i >= 0)
			return 1;
		else
			return -1;
	}
	static const name LiquidFloors[] = { 
		"BLOOD1", "BLOOD2", "BLOOD3", "SLIME01", "SLIME02", "SLIME03", "SLIME04", "SLIME05", "SLIME06", "SLIME07", "SLIME08", "SLIME09", "SLIME10", "SLIME11", "SLIME12"
	};
	bool CheckWater(actor checker = null) {
		if (!checker)
			checker = self;
		if (checker.waterlevel > 0)
			return true;
		if (checker.GetFloorTerrain().isLiquid == true)
			return true;
		string flr = TexMan.GetName(checker.floorpic);
		if (flr.IndexOf("WATER") >= 0 || flr.IndexOf("LAVA") >= 0 || flr.IndexOf("NUKAGE") >= 0)
			return true;
		for (int i = 0; i < LiquidFloors.Size(); i++) {
			if (flr == LiquidFloors[i])
				return true;
		}
		return false;
	}
	override void BeginPlay() {
		super.BeginPlay();
		pi = 3.141592653589793;
	}	
}

//base class for most debris
Class BD_BaseDebris : BDoomActor abstract {
	bool landed;
	name sfxtype; //'debris', 'flames', 'blood', 'gibs' — based on the value the thing gets put into a special array that controls their number, see bd_events.zc
	property sfxtype : sfxtype;
	Default {
		BD_BaseDebris.sfxtype "debris";
		+ROLLSPRITE
		+BLOODLESSIMPACT	//for debris that can bump into actors
		+NOTAUTOAIMED
		+MISSILE			//enters Death when it hits the floor
		+NOBLOCKMAP
		+NOGRAVITY
		+DROPOFF
		+NOTELEPORT
		+FORCEXYBILLBOARD
		+THRUACTORS
		+FLOORCLIP
		+INTERPOLATEANGLES
		-ALLOWPARTICLES
		renderstyle "Translucent";
		alpha 1.0;
		radius 1;
		height 1;
		mass 1;
		damage 0;
	}
	// thanks Gutawer for explaning the math and helping this function come to life
	virtual void FlyBack() {
		if (!target)
			return;
		landed = false;
		bFLATSPRITE = false;
		bNOGRAVITY = false;
		bNOINTERACTION = false;
		bRELATIVETOFLOOR = false;
		bMOVEWITHSECTOR = false;
		bTHRUACTORS = true;
		gravity = 1.0;
		A_FaceTarget();
		
		double dist = Distance2D(target);							//horizontal distance to target
		double vdisp = target.pos.z - pos.z + frandom(8,32);		//height difference between gib and target + randomized height
		double ftime = 20;											//time of flight
		
		double vvel = (vdisp + 0.5 * ftime*ftime) / ftime;
		double hvel = dist / ftime;
		
		Thrust(hvel,angle);
		vel.z = vvel;
	}
	override void PostBeginPlay() {
		if (!level.IsPointInLevel(pos)) {
			destroy();
			return;
		}
		super.PostBeginPlay();
	}
}

//base class for less expensive version for small debris
Class SmallDebris : BD_BaseDebris abstract {
	double wrot;
	double dgravity; //fake gravity
	double dbrake; //how quickly to reduce horizontal speed of "landed" particles
	bool removeonfall; //whether to remove immediately as it reaches the floor
	property dgravity : dgravity;
	property removeonfall : removeonfall;
	property dbrake : dbrake;
	Default {
		+NOINTERACTION
		+CLIENTSIDEONLY
		-MISSILE
		-RELATIVETOFLOOR
		-FLOORCLIP //this flag can screw up pos.z detection
		smalldebris.dgravity 0.8;
		SmallDebris.removeonfall true;
		SmallDebris.dbrake 0;
	}
	override void Tick() {
		super.tick();
		if (isFrozen())
			return;
		if (pos.z > floorz)
			vel.z-=dgravity;
		else if (removeonfall) {
			destroy();
			return;
		}
		else {
			if (!landed) {
				if (pos.z < floorz)
					SetOrigin((pos.x,pos.y,floorz),0);
				vel.z = 0;
				bMOVEWITHSECTOR = true;
				bRELATIVETOFLOOR = true;
				landed = true;
			}
			vel.x *= dbrake;
			vel.y *= dbrake;
		}		
	}
}

//base class for flares and flame effects
Class BD_BaseFlare : BD_BaseDebris abstract {
	protected state mdeath;
	protected state mxdeath;
	name fcolor;		//flare of the color; can be set as a property or externally
	double fscale;		//scale; used to when scale needs to be easily set externally from the spawner
	double falpha;		//alpha; used to when scale needs to be easily set externally from the spawner
	property fcolor : fcolor;
	Default {
		BD_BaseDebris.sfxtype "flames";
		-MISSILE
		+RELATIVETOFLOOR
		+MOVEWITHSECTOR
		+NOINTERACTION
		+BRIGHT
		renderstyle "Add";
		alpha 0.4;
		scale 0.4;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (master) {
			mdeath = master.FindState("Death");
			mxdeath = master.FindState("XDeath");
		}
	}
	virtual void SetColor() { //fcolor is meant to be set by the actor that spawns the flare
		switch (fcolor) {
			case 'red'		: sprite = GetSpriteIndex("LENR"); break;
			case 'green'	: sprite = GetSpriteIndex("LENG"); break;
			case 'blue'		: sprite = GetSpriteIndex("LENB"); break;
			case 'yellow'	: sprite = GetSpriteIndex("LENY"); break;
			case 'tech'		: sprite = GetSpriteIndex("LENS"); break; //tech lamps use a completely different flare
			case 'cbra'		: sprite = GetSpriteIndex("LENC"); break; //so do candelabras
		}
		if (fscale != 0)
			A_SetScale(fscale);
		if (falpha != 0)
			alpha = falpha;
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay SetColor();
			#### # 1;
			wait;
		Loadsprites:
			LENR A 0;
			LENB A 0;
			LENG A 0;
			LENY A 0;
			LENC A 0;
			LENS AB 0;
			SPRK ABC 0;
			stop;
	}
}

// base glow to be spawned alongside projectiles
Class BD_ProjFlare : BD_BaseFlare {
	double xoffset;
	Default {
		BD_BaseDebris.sfxtype "debris";
		BD_BaseFlare.fcolor "Red";
		alpha 0.8;
		scale 0.11;
		-RELATIVETOFLOOR
		-MOVEWITHSECTOR
	}
	override void Tick() {
		super.Tick();
		if (master)
			A_Warp(AAPTR_Master,xoffset,0,0,flags:WARPF_INTERPOLATE);
		if (isFrozen())
			return;
		if (!master || !bdoom_debris || (master && (master.InstateSequence(master.curstate,mdeath) || 
										 master.InstateSequence(master.curstate,mxdeath)) )) {
			Destroy();
			return;
		}
	}
}



Class ParticleFire_General : BD_BaseFlare abstract {
	Default {
		renderstyle "Add";
		scale 0.5;
		alpha 0.5;
		BD_BaseDebris.sfxtype "flames";
	}
	Override void PostBeginPlay() {
		super.PostBeginPlay();
		scale.x*=randompick(-1,1);
	}
}

// Small fire particle that spawns over torches and fades away
Class BD_FireParticle : BD_BaseFlare {
	Default {
		alpha 0.5;
		scale 0.04;
	}
	override void SetColor() {
		if (fcolor == 'green')
			frame = 1;
		else if (fcolor == 'blue')
			frame = 2;
		else if (fcolor == 'yellow')
			frame = 3;
	}
	states	{
		Loadsprites:
			SPRK ABCD 0;
		Spawn:
			SPRK A 0 NoDelay SetColor();
			SPRK # 1 bright {
				A_FadeOut(0.01);
				scale*=0.95;
			}
			wait;
	}
}

Class ParticleFire_Red : ParticleFire_General {
	states {
		Spawn:
			FIPR ABCDEFGHIJKLMNOPQRST 1 bright Nodelay A_FadeOut(0.03);
			stop;
	}
}

Class ParticleFire_Yellow : ParticleFire_General {
	Default {
		alpha 0.6;
		scale 0.35;
	}
	states		{
		Spawn:
			FIPY ABCDEFGHIJKL 2 bright Nodelay A_FadeOut(0.06);
			stop;
	}
}

Class ParticleFire_Green : ParticleFire_Red {
	Default {
	translation "0:3=112:127","5:8=112:127","9:12=112:127","13:15=112:127","16:47=112:127","48:79=112:127","96:111=112:127","112:127=112:127","128:151=112:127","152:159=112:127","160:167=112:127","169:191=112:127","193:207=112:127","208:223=112:127","224:231=112:127","232:235=112:127","236:239=112:127","240:247=112:127","248:249=112:127","250:254=112:127","255:255=112:127";
	}
}

Class FirePiece : BD_BaseFlare abstract {
	Default {
		scale 0.2;
		alpha 0.5;
	}
	states	{
		Spawn:		
			FIRP ABCDEFGHIJ 2 bright NoDelay A_FadeOut(0.05);
			stop;
	}
}



///////////////;
//// SMOKE ////;
///////////////

Class Smoke_General : SmallDebris abstract {
	Default {
		BD_BaseDebris.sfxtype "debris";
		smalldebris.dgravity 0;
		renderstyle "Add";
		alpha 0.3;
		scale 0.1;
	}
	override void PostBeginPlay() {
		if (waterlevel > 1) {
			self.destroy();
			return;
		}
		super.PostbeginPlay();
		roll = random(0,359);
	}
	states	{
		Spawn:
			SMOK ABCDEFGHIJKLMNOPQ 1 NoDelay {
				if (!master)
					A_SpawnItemEx("BasicSmokeTrail",frandom(-2,2),frandom(-2,2),frandom(-2,2),0,0,0,0,SXF_SETMASTER);
				A_Fadeout(0.01);
			}
			stop;
	}
}

Class BasicSmokeTrail : Smoke_General abstract {
	Default {
		alpha 0.15;
	}
	states	{
		Spawn:
			SMOK DEFGHIJKLMNOPQ 1 NoDelay A_Fadeout(0.01);
			stop;
	}
}

//old smoke used by puffs
class ShotSmoke : Smoke_General { 
	Default {
		alpha 0.4;
		scale 0.18;
	}
	states	{
		Spawn:
			SMK1 ABCDEFGHIJKLMNOPQR 1 NoDelay {
				A_SpawnItemEx("ShotSmokeTrail",frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(-1.5,1.5),0,0,0,0,SXF_SETMASTER);
				A_Fadeout(0.01);
			}
			stop;
	}
}

Class ShotSmokeTrail : BasicSmokeTrail {
	Default {
		alpha 0.15;
	}
	states {
		Spawn:
			SMK1 DEFGHIJKLMNOPQR 1 NoDelay A_Fadeout(0.01);
			stop;
	}
}

//spawned over weapon barrels after shooting
class BarrelSmoke : Smoke_General {
	Default {
		alpha 0.05;
		scale 0.02;
	}
	/*override void Tick() {
		if (target) {
			vel.x = target.vel.x;
			vel.y = target.vel.y;
		}
		super.Tick();
	}*/
	states {
		Spawn:
			SMK1 ABCDEF 2 NoDelay A_FadeIn(0.025);
			SMK1 GHIJKLMNOPQR 2 {
				vel.z = clamp(vel.z*0.6,0.04,0.1);
				A_FadeOut(0.005);
				roll+-8;
			}
			stop;
	}
}

//bigger smoke to spawn over heated weapon body
class WeaponSmoke : Smoke_General {
	Default {
		alpha 0.25;
		scale 0.15;
	}
	states	{
		Spawn:		
			SMK1 ABCDEFGHIJKLMNOPQR 1 NoDelay {
				A_SpawnItemEx("WeaponSmokeTrail",random(-2,2),random(-2,2),random(-2,2),0,0,0,0);
				A_FadeOut(0.01);
			}
			stop;
	}
}

Class WeaponSmokeTrail : BasicSmokeTrail {
	Default {
		alpha 0.1;
		scale 0.12;
	}
	states	{
		Spawn:
			SMK1 DEFGHIJKLMNOPQR 1 NoDelay A_Fadeout(0.01);
			stop;
	}
}

//big dark smoke used for some explosions
class BlackSmoke : Smoke_General {
	Default {
		renderstyle "Translucent";
		alpha 0.15;
	}
	states	{
		Spawn:
			SMOK ABCDEFGHIJKLMNOPQ 2 NoDelay {
				A_SpawnItemEx("BlackSmokeTrail",frandom(-2,2),frandom(-2,2),frandom(-2,2));
				A_FadeOut(0.01);
			}
			wait;
	}
}

Class BlackSmokeTrail : BasicSmokeTrail {
Default {
	renderstyle "Translucent";
	alpha 0.08;
}
}

//dark small piece of smoke with a hint of flame — for "dirty" fires like burning barrel
Class BurnSmokePiece : Smoke_General {
Default {
	renderstyle "Translucent";
	alpha 0.3;
	scale 0.05;
}
override void PostBeginPlay() {
	super.PostBeginPlay();
	scale*=frandom(0.6,1);
}
states {
	Spawn:
		BSMO ABCDEFGHIJKLMNOPQRSTU 1 NoDelay A_FadeOut(0.005);
		stop;
}
}

//even bigger smoke for bigger explosions
class BigBlackSmoke : BlackSmoke {
	Default {
		alpha 0.2;
		scale 0.2;
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay {
				A_SetScale(scale.x*frandom(0.8,1.2)*randompick(-1,1), scale.y*frandom(0.8,1.2)*randompick(-1,1));
			}
			SMOK ABCDEFGHIJKLMNOPQ 2 {
				if (self.GetClassName() == "BigBlackSmoke")
					A_SpawnItemEx("BigBlackSmokeTrail",frandom(-2,2),frandom(-2,2),frandom(-2,2),0,0,0,0,SXF_SETMASTER);
				A_FadeOut(0.02);
				a_Setscale(clamp(scale.x*1.03,default.scale.x,default.scale.x*2.2),clamp(scale.y*1.03,default.scale.y,default.scale.y*2.2));
			}
			wait;
	}
}

Class BigBlackSmokeTrail : BigBlackSmoke {}

//basic smoke behind a rocket
Class BD_RocketTrail : Smoke_General {
	Default {
		scale 0.1;
		renderstyle "Translucent";
		alpha 0.3;
	}
	states {
		Spawn:
			SMOK ABCDEFGHIJKLMNOPQ 1 NoDelay A_FadeOut(0.03);
			stop;
	}
}

Class SmokeBallTrail : BD_RocketTrail {
	Default {
		alpha 0.15;
	}
}

//a particle that spawns thin smoke behind itself
Class SmokingPiece : SmallDebris {
	Default {
		smalldebris.dgravity 0.7;
		alpha 1.0;
	}
	states
			{
			Spawn:
				TNT1 A 1 NoDelay {
					A_SpawnItem("SmokingPieceSmoke");
					A_FadeOut(0.025);
				}
				loop;
		}
}

Class SmokingPieceSmoke : BasicSmokeTrail {
	Default {
		scale 0.04;
		alpha 0.3;
	}
}

///////////////
//// PUFFS ////
///////////////

//smoke coming out of wood walls
Class WallSmoke : Smoke_General {
	Default {
		+ROLLCENTER
		scale 0.018;
		renderstyle "Translucent";
		alpha 0.3;
	}
	override void PostBeginPlay()
		{
		super.PostBeginPlay();
		A_SetScale(frandom(0.028,0.032),frandom(0.028,0.032));
		wrot = (random(3,10)*randompick(-1,1));
	}
	states {
		Spawn:
			SM21 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 NoDelay {
				A_Fadeout(0.006);
				a_Setscale(clamp(scale.x*1.02,0.01,0.1),clamp(scale.y*1.02,0.01,0.1));
				vel*=0.97;
				roll+=wrot;
				wrot *= 0.98;
			}
			SM22 ABCDEFGHIJK 2 {
				alpha*=0.99;
				a_Setscale(clamp(scale.x*1.01,0.01,0.1),clamp(scale.y*1.01,0.01,0.1));
				vel*=0.93;
				roll+=wrot;
				wrot *= 0.97;
				if (alpha < 0.1)
					A_FadeOut(0.005);
			}
			wait;
	}
}

//circular puff of smoke for hard walls (stone, brick)
Class RingSmoke : WallSmoke {
	Default {
		alpha 0.4;
	}
	states {
		Spawn:
			SM21 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 NoDelay {
				A_Fadeout(0.015);
				a_Setscale(clamp(scale.x*1.01,0.01,0.05),clamp(scale.y*1.01,0.01,0.05));
				vel*=0.9;
				roll+=wrot;
				wrot *= 0.96;
			}
			SM22 ABCDEFGHIJK 1 {
				A_Fadeout(0.01);
				a_Setscale(clamp(scale.x*1.01,0.01,0.05),clamp(scale.y*1.01,0.01,0.05));
				vel*=0.9;
				roll+=wrot;
				wrot *= 0.95;
				if (alpha < 0.1)
					A_FadeOut(0.005);
			}
			wait;
			
			/*SM21 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 NoDelay {
				A_Fadeout(0.005);
				a_Setscale(clamp(scale.x*1.01,0.01,0.1),clamp(scale.y*1.01,0.01,0.1));
				vel*=0.9;
				roll+=wrot;
			}
			SM22 ABCDEFGHIJK 1 {
				A_Fadeout(0.005);
				a_Setscale(clamp(scale.x*1.01,0.01,0.1),clamp(scale.y*1.01,0.01,0.1));
				vel*=0.9;
				roll+=wrot;
			}
			wait;*/
	}
}

// Base puff class. Also holds arrays with texture names to simulate different effects for different "surfaces"
Class BdoomPuff : BDoomActor abstract {
	Default {
		+NOBLOCKMAP
		+NOGRAVITY
		+FORCEXYBILLBOARD
		-ALLOWPARTICLES
		+DONTSPLASH
		-FLOORCLIP
		radius 1;
		height 1;
	}
	static const string LiquidWalls[] = {
		"WATER", "FALL", "BLOOD"
	};
	static const string HotWalls[] = {
		"LAVA", "FIRE", "NUKAGE", "DBRAIN"
	};
	static const string WoodWalls[] = {
		"WOOD", "PAN", "CRAT", "PANEL", "STUCCO", "FLAT5_1", "FLAT5_2", "CEIL1_1", "FLOOR0_1", "FLOOR0_2", "FLOOR0_3", "FLOOR7_1", "FLAT5_5", "TANROCK2", "TANROCK3", "TANROCK8", "ZZWOLF5", "ZZWOLF6", "ZZWOLF7", "BIGDOOR5", "BIGDOOR6", "BIGDOOR7", "ZZWOLF5"
	};
	static const string MetalWalls[] = {
		"METAL", "SHAWN", "SILVER", "BRONZ", "PIPE", "SUPPORT", "TEK", "ZZZFACE6", "ZZZFACE7", "ZZZFACE8", "ZZZFACE9", "FLOOR0_6", "FLOOR0_7", "FLOOR3_3", "FLOOR4_1", "FLOOR4_5", "FLOOR4_8", "FLOOR5_1", "STEP", "CEIL5_2", "FLAT22", "FLAT23", "CONS", "CEIL1_1", "CEIL1_2", "CEIL1_1", "SLIME14", "SLIME15", "SLIME16", "LITE", "COMP", "COMM", "SW2MET", "BIGDOOR1", "BIGDOOR2", "BIGDOOR3", "BIGDOOR4", "DOOR", "EXITDOOR", "SPCDOOR", "BRNSMAL", "COMP", "MIDB", "MIDS", "PLAT"
	};
	static const string BrickWalls[] = {
		"BRICK", "BIGBRIK"
	};
	static const string MarbleWalls[] = {
		"MARB", "GST", "DEM", "FLOOR7_2"
	};
	static const string GoreWalls[] = {
		"SK_", "SKIN2", "SKINCUT", "SKINEDGE", "SKINFACE", "SFLR", "SPINE", "SNAKE", "SLOPPY", "SP_FACE"
	};
	static const string SkinWalls[] = {
		"SKIN", "ZFACE"
	};
}

//used by stuff like decorative rails spawned by BFGball
Class NullPuff : Actor {
	Default {
		decal "none";
		+NODECAL
		+NOINTERACTION
		+BLOODLESSIMPACT
		+PAINLESS
		+PUFFONACTORS
		+NODAMAGETHRUST
	}
	states {
		Spawn:
			TNT1 A 1;
			stop;
	}
}

// Spawned by hitscans and real bullets (when in Modern Guns mode). Simulates different effects based on texture name
Class EnBulletPuff : BDoomPuff {
	protected string hittex; 			//name of texture that was hit. Needs to be string to use IndexOf for partial matches
	name DebrisType;					//debris type (different shape/colors for wood, bricks, etc.)
	protected int rchance; 				//ricochet chance
	protected bool debris; 				//whether to spawn debris
	protected int smoke; 				//0 - none, 1 - long directional, 2 - circular
	protected int sparkchance; 			//spark spawn chance
	protected double hitangle;			//angle at which the bullet hit a line
	protected string decaltype;			//type of decals to leave (wood hole, metal dent, stone chip, generic hole). Decals are strings, not names
	protected FLineTraceData puffdata;	//gets all the data about hit location
	Default {
		+NOINTERACTION
		+PUFFGETSOWNER
		decal "none";
		renderstyle "Add";
		scale 0.075;
		alpha 0.8;
		mass 1;
		height 1;
		radius 1;
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay {
				// First we fire linetrace, using angle and pitch of target (which is either the player or the bullet projectile)
				if (target) {
					angle = target.angle;
					pitch = target.pitch;
				}
				// When spawned by hitscans and by bullets pos.z ends up different. Make sure linetrace is not executed inside floor/ceiling
				double traceheight = pos.z;
				if (pos.z <= floorz)
					traceheight = floorz+1;
				else if (pos.z >= ceilingz)
					traceheight = ceilingz-1;
				//console.printf("posz: %d | floorz: %d | ceilingz: %d | angle: %d | pitch: %d",pos.z,floorz,ceilingz,angle,pitch);
				LineTrace(angle,16,pitch,TRF_THRUACTORS|TRF_NOSKY|TRF_ABSPOSITION,traceheight,pos.x,pos.y,data:puffdata);	
				if (puffdata.HitType == TRACE_HitFloor)
					hittex = texman.GetName(floorpic);
				else if (puffdata.HitType == TRACE_HitCeiling)
					hittex = texman.GetName(ceilingpic);
				else if (puffdata.HitType == TRACE_HitWall) {
					hittex = texman.GetName(puffdata.HitTexture);			
					// if hit a wall, check which side of the line was hit, then set the angle at which the smoke and debris will come out
					if (target) {
						double lineangle = atan2(puffdata.HitLine.delta.y, puffdata.HitLine.delta.x);
						if (puffdata.lineside == 0)
							hitangle = lineangle - 90;
						else
							hitangle = lineangle + 90;
					}
				}
				
				// regular liquids — splash sound, no visuals
				for (int i = 0; i < LiquidWalls.Size(); i++) {
					if (hittex.IndexOf(LiquidWalls[i]) >= 0 ) {
						A_PlaySound("world/watersplashsmall",0,1.0,0,3);
						//console.Printf("Liquid Texture: %s",hittex);
						self.destroy();
						return ResolveState(null);
					}
				}
				// hot liquids — sizzle sound, no visuals
				for (int i = 0; i < HotWalls.Size(); i++) {
					if (hittex.IndexOf(HotWalls[i]) >= 0 ) {
						A_PlaySound("world/lavasizzlesmall",0,1.0,0,3);
						//console.Printf("Hot Texture: %s",hittex);
						self.destroy();
						return ResolveState(null);
					}
				}
				// wood walls — long smoke, no ricochet/sparks, decals are textured like wood
				for (int i = 0; i < WoodWalls.Size(); i++) {
					if (hittex.IndexOf(WoodWalls[i]) >= 0 ) {
						decaltype = "BulletWchip";
						DebrisType = 'wood';
						debris = true;
						smoke = 1;
						rchance = 0;
						sparkchance = 0;
						A_Playsound("bullet/wood",0,1.0,0,3);	
						//console.Printf("Wood Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}
				// Marble walls — same as stone but spawns marble-colored green/grey debris
				for (int i = 0; i < MarbleWalls.Size(); i++) {
					if (hittex.IndexOf(MarbleWalls[i]) >= 0 ) {
						decaltype = "BulletSchip";
						DebrisType = 'marble';
						rchance = 20;
						debris = true;
						smoke = 2;
						A_Playsound("bullet/stone",0,1.0,0,3);	
						//console.Printf("Marble Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}
				// Metallic walls - high ricochet chance, high spark chance, no smoke, decals are shallow dents
				for (int i = 0; i < MetalWalls.Size(); i++) {
					if (hittex.IndexOf(MetalWalls[i]) >= 0 ) {
						decaltype = "BulletMchip";
						rchance = 40;
						sparkchance = 50;
						debris = false;
						smoke = 0;
						A_Playsound("bullet/metal",0,1.0,0,3);	
						//console.Printf("Metal Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}
				// Brick walls - low ricochet chance, no sparks, circular smoke, decals are big shallow chips
				for (int i = 0; i < BrickWalls.Size(); i++) {
					if (hittex.IndexOf(BrickWalls[i]) >= 0 ) {
						decaltype = "BulletSchip";
						DebrisType = 'brick';
						rchance = 15;
						debris = true;
						smoke = 2;
						A_Playsound("bullet/stone",0,1.0,0,3);	
						//console.Printf("Brick Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}	
				// Walls made of meat/gore - no ricochet, no sparks, no smoke, hits with a flesh sound, spawns a little blood
				for (int i = 0; i < GoreWalls.Size(); i++) {
					if (hittex.IndexOf(GoreWalls[i]) >= 0 ) {
						decaltype = "Bulletchip";
						DebrisType = 'bloody';
						debris = true;
						smoke = 0;
						A_Playsound("bullet/body",0,1.0,0,3);	
						//console.Printf("Gore Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}	
				// Dry skin walls - same as gore walls but no boood
				for (int i = 0; i < SkinWalls.Size(); i++) {
					if (hittex.IndexOf(SkinWalls[i]) >= 0 ) {
						decaltype = "Bulletchip";
						DebrisType = 'skin';
						debris = false;
						smoke = 0;
						A_Playsound("bullet/body",0,1.0,0,3);	
						//console.Printf("Skin Texture: %s",hittex);
						return ResolveState("PuffSpawn");
					}
				}	
				//Generic - suitable for stone, gray walls, etc. Minor ricochet chance, no sparks, generic debris, circular smoke
				if (puffdata.HitType != TRACE_HitNone) {
					decaltype = "Bulletchip";
					DebrisType = 'stone';
					rchance = 25;
					debris = true;
					smoke = 2;
					A_Playsound("bullet/stone",0,1.0,0,3);	
					//console.Printf("Generic Texture: %s",hittex);	
					return ResolveState("PuffSpawn");		
				}
				return ResolveState(null);
			}
			goto PuffSpawn;
		PuffSpawn:
			TNT1 A 0 {
				if (bdoom_debris) {
					if (random(0,100) < rchance)
						A_SpawnProjectile("RicochetBullet",0,0,random(0,359),CMF_AIMDIRECTION,random(-45,45));
					if (smoke == 1) {
						if (puffdata.HitType == TRACE_HitFloor) {
							for (int i = 0; i < 10; i++)
								A_SpawnItemEx("WallSmoke",0,0,0,frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.9),0,0,48);
						}
						else if (puffdata.HitType == TRACE_HitCeiling) {
							for (int i = 0; i < 10; i++)
								A_SpawnItemEx("WallSmoke",0,0,0,frandom(-0.1,0.1),frandom(-0.1,0.1),-frandom(0.1,0.9),0,0,48);
						}
						else {
							for (int i = 0; i < 10; i++)
								A_SpawnItemEx("WallSmoke",0,0,0,frandom(0.1,0.9),0,0,hitangle+frandom(-4,4),SXF_ABSOLUTEANGLE,48);
						}
					}
					else if (smoke == 2) {
						for (int i = 0; i < 15; i++)
							A_SpawnItemEx("RingSmoke", 0,0,0, frandom(-0.7,0.7),frandom(-0.7,0.7),frandom(-0.7,0.7),0,0,64);
					}
					if (debris) {
						if (puffdata.HitType == TRACE_HitFloor) {
							for (int i = 0; i < 5; i++)
								A_SpawnItemEx("Wallpart", 0,0,0, frandom(-1,1),frandom(-1,1),frandom(2,5), 0,SXF_SETTARGET,64);
						}
						else if (puffdata.HitType == TRACE_HitCeiling) {
							for (int i = 0; i < 5; i++)
								A_SpawnItemEx("Wallpart", 0,0,0, frandom(-1,1),frandom(-1,1),0, 0,SXF_SETTARGET,64);
						}
						else {
							for (int i = 0; i < 5; i++)
								A_SpawnItemEx("Wallpart", 0,0,0, frandom(2,5),0,frandom(1,3), hitangle+frandom(-15,15),SXF_SETTARGET|SXF_ABSOLUTEANGLE,64);
						}
					}
					if (random(0,100) < sparkchance) {
						for (int i = 0; i < 6; i++)
							A_SpawnItemEx("RicochetSpark", 0,0,0, frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(1,4), 0,0,72);
					}
				}
				if (decaltype) {
					//spawn an actor with 3D model to emulate floor/ceiling decals
					if (puffdata.HitType == TRACE_HitFloor && !CheckLandingSize(2)) {
						let pld = BD_PlaneDecal(Spawn("BD_PlaneDecal",(pos.x,pos.y,floorz)));
						if (pld) pld.decaltype = decaltype;
					}
					else if (puffdata.HitType == TRACE_HitCeiling && !CheckLandingSize(2,true)) {
						let pld = BD_PlaneDecal(Spawn("BD_PlaneDecal",(pos.x,pos.y,ceilingz)));
						if (pld) {
							pld.decaltype = decaltype;
							pld.roll = 180;
						}
					}
					//or spray the decal we need
					else
						A_SprayDecal(decaltype,8);
				}
				return ResolveState(null);
			}
			BPUF ABCD 1 bright;
			stop;
	}
}

//decal to spawn on the floors and ceilings
Class BD_PlaneDecal : BD_BaseDebris {
	name decaltype;
	Default {
		+NOINTERACTION
		+RELATIVETOFLOOR
		+MOVEWITHSECTOR
		renderstyle 'Shaded';
	}
	states {
		//even though these are not real sprites, they still need to be defined for MODELDEF to attach the model properly
		load:
			CHIP ABCDE 0;
			METL ABCDE 0;
			WOOD ABCDE 0;
			BRIK ABCD 0;
		Spawn:
			TNT1 A 0 NoDelay {				
				switch (decaltype) {
					case 'BulletChip'	: sprite = GetSpriteIndex("CHIP");	frame = random(0,4); alpha = 0.85; angle = random(0,360); break;
					case 'BulletMChip'	: sprite = GetSpriteIndex("METL");	frame = random(0,4); alpha = 0.55; angle = random(0,360);break;
					case 'BulletWChip'	: sprite = GetSpriteIndex("WOOD");	frame = random(0,4); alpha = 1.0; break;
					case 'BulletSChip'	: sprite = GetSpriteIndex("BRIK");	frame = random(0,3); alpha = 0.75; angle = random(0,360);break;
				}
			}
			#### # -1;
			stop;
	}
}

//a very small dark piece of debris spawned by hitting a wall
Class WallPart : SmallDebris {
	Default {
		SmallDebris.removeonfall false;
		SmallDebris.dbrake 0.8;
		-NOGRAVITY
		Renderstyle "Translucent";
		alpha 1.0;
		scale 0.16;
		smalldebris.dgravity 0.4;
		translation "DebrisStone";
	}
	override void PostBeginPlay()
		{
		super.PostBeginPlay();
		wrot = random(8,14)*randompick(-1,1);
		scale*=frandom(0.8,1.2)*randompick(-1,1);
		if (!target || !(target is "EnBulletPuff"))
			return;
		let puff = EnBulletPuff(target);
		if (puff.DebrisType == 'wood') {
			A_SetTranslation("DebrisWood");
			A_setScale(frandom(0.14,0.18),frandom(0.06,0.09));
		}
		else if (puff.DebrisType == 'marble') {
			if (random(0,1) == 1)
				A_SetTranslation("DebrisMarble1");
			else
				A_SetTranslation("DebrisMarble2");
		}
		else if (puff.DebrisType == 'brick')
			A_SetTranslation("DebrisBrick");
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay {
				if (target && target is "EnBulletPuff") {
					let puff = EnBulletPuff(target);
					if (puff.DebrisType == 'bloody')
					return ResolveState("Bloody");
				}
				return ResolveState(null);
			}
			BPRT AAAAAAAAAAAAAAAAAAAAAAAAA 1 {
				A_FadeOut(0.005);
				scale*=0.98;
				if (pos.z > floorz)
					roll+=wrot;
			}
			wait;
		Bloody:
			TNT1 A 0 {
				A_SetScale(0.1,0.1);
				static const string bsprite[] =
				{
					"YBL7","YBL8","YBL9"
			};
				self.sprite = GetSpriteIndex(bsprite[random(0,2)]);
			}
			#### ABCDEFGHIJKLMNOPQRS 1 A_Fadeout(0.08);
			stop;		
	}
}

Class BD_GlassPiece : WallPart {
	Default {
		renderstyle "Add";
		alpha 0.4;
	}
	states {
		Bloody:
			stop;
	}
}



Class RocketDebris : WallPart {
	Default {
		smalldebris.dgravity 0.75;
		scale 0.24;
	}
}

//small spark spawned by chainsaw, knuckle, bullets on metal
Class RicochetSpark : SmallDebris {
	Default {
		SmallDebris.removeonfall false;
		SmallDebris.dbrake 0.9;
		renderstyle "Add";
		alpha 1.0;
		radius 3;
		height 3;
		scale 0.03;
	}
	override Void PostBeginPlay() {
		if (waterlevel > 1) {
			self.destroy();
			return;
		}
		super.PostbeginPlay();
	}
	states {
		Spawn:
			SPRK A 1 bright	{
				A_FadeOut(0.03);
				scale *= 0.95;
			}
			loop;
	}
}



//fake bullet that ricochets from bulletpuffs
Class RicochetBullet : BD_BaseDebris {
	Default {
		+NOINTERACTION
		//+THRUACTORS
		//+DONTSPLASH
		//bouncetype "Hexen";
		speed 40;
		seesound "none";
		deathsound "none";
		renderstyle "Add";
		alpha 0.8;
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay {
				//if (bdoom_sparks == 1)
					//A_PlaySound("weapons/ricochet");
			}
			PUFF A 2;
			stop;
	}
}


Class SphereSpawner : Actor {
	int langle;
	int lpitch;
	Default {
		+NOINTERACTION
		+FORCEXYBILLBOARD
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		langle = 0;
		lpitch = -90;
	}
	states {
		Spawn:
			TNT1 A 1 NoDelay {
				while (langle <= 350) {
						while (lpitch <= 90) {
							FLineTraceData ltr;
							LineTrace(langle,8,lpitch,TRF_THRUACTORS,data:ltr);
							let vec = level.vec3diff (pos,ltr.hitlocation);						
							A_SpawnParticle("0000FF",SPF_FULLBRIGHT,100,1,0,vec.x,vec.y,vec.z);
							lpitch+=10;
						}
						lpitch = -90;
						langle+=10;
				}
			}
			stop;
	}
}