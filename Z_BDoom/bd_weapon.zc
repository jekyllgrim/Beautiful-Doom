Class BDoomWeapon : DoomWeapon abstract
{
int offsetTics; // current camera sway timer
double maxOffsetTics; // starting point for the timer
double offsetAngle; // the target angle for the camera sway
double offsetPitch;
double currentAngleOffset; // how much has the camera already been offset?
double currentPitchOffset;
property maxOffsetTics : maxOffsetTics;
property offsetTics : offsetTics;

Default {  //required default values for A_CameraSway. Otherwise it'll crash due to division by zero
	BDoomWeapon.maxOffsetTics 1;
	BDoomWeapon.offsetTics -1;
	}

// Camera sway function by josh771
action void A_CameraSway(double aoffset, double poffset, int tics)
{
    invoker.offsetTics = tics;
    invoker.maxOffsetTics = tics;
    
    invoker.offsetAngle = aoffset;
    invoker.offsetPitch = poffset;

    invoker.currentAngleOffset = 0.0;
    invoker.currentPitchOffset = 0.0;
}

override void DoEffect()
	{
	Super.DoEffect();
	if (owner) {
		if(bdoom_weapons == 2) {
				BobRangeX = 0.2;
				BobRangeY = 0.2;
				BobStyle = 5;
				BobSpeed = 2.0;
			}
		else {
				BobRangeX = 1.0;
				BobRangeY = 1.0;
				BobStyle = 0;
				BobSpeed = 1.0;
			}
		if (offsetTics >= 0) {
			double phase = (offsetTics / maxOffsetTics) * 90.0;
			double newAngleOffset = (cos(phase) * offsetAngle);
			double newPitchOffset = (cos(phase) * offsetPitch);
			double finalAngle = (owner.angle - currentAngleOffset) + newAngleOffset;
			double finalPitch = (owner.pitch - currentPitchOffset) + newPitchOffset;
			currentAngleOffset = newAngleOffset;
			currentPitchOffset = newPitchOffset;
			owner.A_SetAngle(finalAngle, SPF_INTERPOLATE);
			owner.A_SetPitch(finalPitch, SPF_INTERPOLATE);
			offsetTics--;
			}
		}
	}

action void BD_WeaponReady(bool allowsecondary = false)
	{
	if (invoker.ammo2 && invoker.ammo2.amount > 0 && invoker.ammo1.amount < invoker.ammo1.maxamount) 
		{
		if (allowsecondary)	
			A_WeaponReady(WRF_ALLOWRELOAD);
		else
			A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
	else 
		{
		if (allowsecondary)	
			A_WeaponReady();
		else
			A_WeaponReady(WRF_NOSECONDARY);
		}
	
	}

action void BD_Reload()
	{
	if (invoker.ammo2)
		{
		while (invoker.ammo2.amount > 0 && invoker.ammo1.amount < invoker.ammo1.maxamount)
			{
			TakeInventory(invoker.ammo2.GetClass(),1);
			GiveInventory(invoker.ammo1.GetClass(),1);
			}
		}
	}

//Casing spawn function with cool math by Marisa Kirisame (see mk_matrix)
action void A_Eject(class<Actor> itemtype, double xofs = 0, double yofs = 0, double zofs = 0, double xvel = 0, double yvel = 0, double zvel = 0)
	{
		Vector3 ofs = player.mo.pos+(0,0,player.viewheight);
		Vector3 x, y, z;
		[x, y, z] = Matrix4.getaxes(player.mo.pitch,player.mo.angle,player.mo.roll);
		let c = Spawn(itemtype,ofs+x*xofs+y*yofs-z*zofs);
		c.vel = x*xvel+y*yvel+z*zvel;
		c.target = self; //if spawned by monster it'll disappear after bouncing
		c.angle = angle;
	}
}


//////////////

Class BulletTracer : FastProjectile
{
Default {
	-ACTIVATEIMPACT;
	-ACTIVATEPCROSS;
	+NOTELEPORT;
	+BLOODLESSIMPACT;
	+THRUGHOST;
	alpha 0.75;
	renderstyle "add";
	damage 0;
	speed 150;
	radius 4;
	height 4;
	seesound "null";
	deathsound "null";
	}    
//override snippet by phantombeta
override void Tick () {
        Super.Tick ();

        if (!playeringame [consolePlayer])
            return;
        
        let curCamera = players [consolePlayer].camera;
        if (!curCamera) // If the player's "camera" variable is null, set it to their PlayerPawn
            curCamera = players [consolePlayer].mo;
        if (!curCamera) // If the player's PlayerPawn is null too, just stop trying
            return;

        if (CheckIfCloser (curCamera, 192))
            A_PlaySound("weapons/tracerwhizz",1,1.0,0,8);
    }
states
	{
	Spawn:
		PUFF A 1 bright {
			if (bdoom_tracers == 0)
				self.destroy();
			}
		loop;
	Xdeath:
		TNT1 A 1;
		stop;
	Death:
		TNT1 A 1 {
			if (frandom(0.0,1.0) > 0.8)
				A_SpawnProjectile("RicochetBullet",0,0,random(0,360),2,random(-40,40));
			}
		stop;
	}
}

Class RealBullet : BulletTracer
{
double bulletdamage;
double falloffstrength;
double mindamage;
int falloffdistance;
int bulletspeed;
int hitangle;
property bulletspeed : bulletspeed;
property falloffstrength : falloffstrength;
property falloffdistance : falloffdistance;
property mindamage : mindamage;
Default {
	//RealBullet.bulletspeed 200;
	RealBullet.FalloffDistance 1024;
	RealBullet.falloffstrength 0.3;
	RealBullet.mindamage 1;
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT
	-BLOODLESSIMPACT
	+BLOODSPLATTER
	+THRUGHOST
	decal "none";
	speed 170;
	radius 1;
	height 1;
	DamageFunction bulletdamage;
	}
override void PostBeginPlay()
	{
	super.PostBeginPlay();
	//vel = vel.Unit () * bulletspeed;
	BulletDamage = 3 * frandom(4,8);
	}
override void Tick() {
	super.Tick();
	if (target && distance3d(target) > falloffdistance)
		bulletdamage = clamp(bulletdamage*falloffstrength,mindamage,100);
	//console.printf("Damage %f",bulletdamage);
	}
states
	{
	Death:
		TNT1 A 1 A_SpawnItemEx("EnBulletPuff",0,0,0,0,0,0,0,SXF_SETTARGET|SXF_ORIGINATOR);
		stop;
	}
}

Class BD_PistolBullet : RealBullet
{
Default {
	//RealBullet.bulletspeed 170;
	speed 170;
	RealBullet.FalloffDistance 512;
	RealBullet.falloffstrength 0.9;
	}
override void PostBeginPlay()
	{
	super.PostBeginPlay();
	BulletDamage = 15; //3 * frandom(4,8);
	}
}

Class BD_RifleBullet : RealBullet
{
Default {
	//RealBullet.bulletspeed 230;
	speed 230;
	RealBullet.FalloffDistance 640;
	RealBullet.falloffstrength 0.93;
	}
override void PostBeginPlay()
	{
	super.PostBeginPlay();
	BulletDamage = 15; //3 * frandom(6.5,8.0);
	}
}

Class BD_ShotgunPellet : RealBullet
{
Default {
	//RealBullet.bulletspeed 140;
	speed 140;
	RealBullet.FalloffDistance 400;
	RealBullet.falloffstrength 0.85;
	RealBullet.mindamage 0.4;
	}
override void PostBeginPlay()
	{
	super.PostBeginPlay();
	BulletDamage = 12; //3 * frandom(3,5);
	}
}

Class BD_SSGPellet : RealBullet
{
private int ripdepth; //how much health it can pierce through
private actor hitvictim;
Default {
	//RealBullet.bulletspeed 140;
	damage 0;
	speed 140;
	RealBullet.FalloffDistance 300;
	RealBullet.falloffstrength 0.8;
	RealBullet.mindamage 0.5;
	+RIPPER
	//+NOBOSSRIP
	}
override void Tick() {
	super.Tick();
	if (ripdepth <= 0)
		self.destroy();
	}
override int specialmissilehit(actor victim) {
	if (victim && !victim.player) {
		ripdepth = ripdepth - victim.health; //reduce ripping ability before anything else
		if (victim != hitvictim) { //deal damage only ONCE even if the pellet rips through
			hitvictim = victim;
			victim.DamageMobj(self,target,bulletdamage,"Normal");
			//console.PrintF("Ripdepth %d, Health %d",ripdepth,victim.health);
			}
		}
	if (ripdepth <= 0)
		return -1;
	else
		return 1;
	//bulletdamage*= 0.5;
	}
override void PostBeginPlay()
	{
	super.PostBeginPlay();
	BulletDamage = 12;
	ripdepth = 200;
	}
}


//***************
// EMPTY CASINGS
//***************



Class Casing_General : DebrisGeneral
{
Default {
	-FLOORCLIP
	-THRUACTORS
	-NOGRAVITY
	-THRUACTORS
	+ROLLCENTER
	+BOUNCEONACTORS
	-FLOORCLIP
	-RELATIVETOFLOOR
	renderstyle "normal";
	bouncetype "doom";
	seesound "none";
	mass 1;
	Radius 1;
	Height 1;
	bouncefactor 0.5;
	Scale 0.15;
	}
states
	{
	Spawn:
		CAS1 A 0 NoDelay {
			if (self is "PistolCasing")
				self.sprite = GetSpriteIndex("CAS1");
			else if (self is "ShotgunCasing")
				self.sprite = GetSpriteIndex("CAS2");
			else if (self is "ChaingunCasing")
				self.sprite = GetSpriteIndex("CAS3");
			roll = random(88,92);
			}
	Fly:
		#### AABBCCBBAABBCCBBAABBCCBBAABBCCBB 1 {
			if(!target)
				self.destroy();
			roll+=22;
			if (waterlevel > 0)
				return ResolveState("Death");
			return ResolveState(null);	
			}
		goto death;
	Death:
		#### # 1 {
			roll = (randompick(0,180) + random(-8,8));
			if (target && target.bISMONSTER)
				self.destroy();
			A_QueueCorpse();
			A_Stop();
			ClearBounce();
			//bNOINTERACTION = true;
			//bRELATIVETOFLOOR = true;
			}
		#### # -1;
		stop;
	loadsprites:
		CAS1 ABC 0;	CAS2 ABC 0;	CAS3 ABC 0;
		stop;
	}
}


Class PistolCasing : Casing_General
{
Default {
	Scale 0.11;
	bouncesound "weapons/casing";
	}
}

Class ShotgunCasing : Casing_General
{ 
Default {
	Scale 0.2;
	bouncesound "weapons/shotgunshells";
	}
}

Class ChaingunCasing : Casing_General
{ 
Default {
	Scale 0.15;
	bouncesound "chaingun/casing";
	}
}
