Class BD_ChaingunGuy : BDoomMonster {
	private bool attacking;
	private state ddeath1;
	Default {
		species "ChaingunGuy";
		+DOHARMSPECIES
		Health 70;
		gibhealth 40;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		SeeSound "chainguy/sight";
		PainSound "chainguy/pain";
		DeathSound "chainguy/death";
		ActiveSound "chainguy/active";
		AttackSound "none";
		Obituary "$OB_CHAINGUY";
		Tag "$FN_HEAVY";
		Dropitem "Chaingun";
		BdoomMonster.monsterclip 40;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		ddeath1 = FindState("Death1");
	}
	override void Tick() {
		super.Tick();
		if (isFrozen() || bdoom_weapons != 2)
			return;
		//if (ppain && InStateSequence(curstate,ppain))
			//console.printf("ouch");
		if (ssee && InStateSequence(curstate,ssee))
			sprite = GetSpriteIndex('RPO1');
		else if (ppain && InStateSequence(curstate,ppain))
			sprite = GetSpriteIndex('RPOG');
		else if (ddeath && InStateSequence(curstate,ddeath))
			sprite = GetSpriteIndex('RPOM');
		else if (ddeath1 && InStateSequence(curstate,ddeath1))
			sprite = GetSpriteIndex('RPON');
		else if (vxdeath && InStateSequence(curstate,vxdeath))
			sprite = GetSpriteIndex('RGIB');
	}
	states {
	Spawn:
		CPAS A 10 {
			if (bdoom_weapons == 2)
				sprite = GetSpriteIndex("RPAS");
			A_Look();
		}
		CPAR A 10 {
			if (bdoom_weapons == 2)
				sprite = GetSpriteIndex("RPAR");
			A_Look();
		}
		loop;
	AttackEnd:
		TNT1 A 0 {
			A_Playsound("weapons/chaingun/stop",5);
			attacking = false;
		}
	See:
		TNT1 A 0 {
			if (attacking) {
				attacking = false;
				A_StopSound(5);
			}
		}
		CPO1 ABCDDCBA 3 A_Chase();
		loop;
	Missile:
		TNT1 A 0 {
			if (bdoom_weapons == 2)
				return ResolveState("MissileModern");
			if (bdoom_weapons > 0) {
				A_Playsound("weapons/chaingun/start",5);
				attacking = true;
			}
			return ResolveState(null);
		}
		CPAS ABCEE 2 A_FaceTarget();
	MissileLoop:
		CPAS F 2 BD_BulletAttack(22.5, 0, 1, random(1,5)*3, "chainguy/attack", "weapons/chaingun/fire","ChaingunCasing",18,-20,34,32,12);
		CPAS E 2 A_FaceTarget();
		CPAS F 2 BD_BulletAttack(22.5, 0, 1, random(1,5)*3, "chainguy/attack", "weapons/chaingun/fire","ChaingunCasing",18,-20,34,32,12);
		CPAS E 2 A_FaceTarget();
		CPAS E 1 A_MonsterRefire(40,"AttackEnd");
		loop;
	MissileModern:
		RPAS BCDD 2 A_FaceTarget();
	MissileModernDo:
		RPAS E 1 {
			if (bdoom_debris)
				A_SpawnItemEx("ChaingunCasing",14,-15,32,		frandom(2,4),0,random(2,4),	random(-85,-95),SXF_SETTARGET);
			A_PlaySound("Mweapons/rifle/fire");
			A_CustomBulletAttack(20.0,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",30,4);
			monsterclip--;
		}
		RPAS FD 2 A_FaceTarget();
		TNT1 A 0 BD_Refire(4,"MissileModernEnd");
		goto MissileModernDo;
	MissileModernEnd:
		RPAS DCB 3;
		goto See;
	Reload:
		TNT1 A 0 {
			bFRIGHTENED = true;
			A_PlaySound("Mweapons/rifle/clipout");
			monsterclip = 32;
		}
		RPO1 ABCDDCBA 3 A_Chase(null,null);
		RPO1 ABCDDCBA 3 A_Chase(null,null);
		TNT1 A 0 A_PlaySound("Mweapons/rifle/clipin");
		RPO1 ABCDDCBA 3 A_Chase(null,null);
		TNT1 A 0 { bFRIGHTENED = false; }
		goto see;
	Pain:
		TNT1 A 0 {
			if (attacking && bdoom_weapons != 2) {
				attacking = false;
				A_Playsound("weapons/chaingun/stop",5);
			}
		}
		CPOG a 3;
		CPOG b 3 a_pain();
		goto see;
	Death: //vanilla
		TNT1 A 0 {
			if (attacking && bdoom_weapons != 2) {
				attacking = false;
				A_Playsound("weapons/chaingun/stop",6);
			}
			return A_Jump(128,"Death1");
		}
		CPOM a 3 BD_Bleed();
		CPOM b 3 A_scream();
		CPOM c 3 BD_Bleed();
		CPOM de 2 BD_Bleed();
		CPOM f 3 A_NoBlocking();
		CPOM ghi 3;
		CPOM jklM 2;
		goto deathbleed;
	Death1: //fall backwards
		CPON A 3 A_scream();
		CPON B 3 BD_Bleed();
		CPON C 3 A_NoBlocking(); 
		CPON D 3 BD_Bleed();
		CPON EFG 2;
		CPON GHI 2 BD_Bleed();
		CPON JK 2;
		goto deathbleed;
	Death.Saw:
		CPOY ABCDE 2;
		TNT1 A 0 A_NoBlocking();
		CPOY FGH 2;
		CPOY IJKLMN 3 {
			if(bdoom_blood) {
				A_FaceTarget();
				A_SpawnItemEx("Blood",	frandom(-4,4),frandom(0,6),frandom(28,36),frandom(2,4),0,frandom(4,6),	random(-80,-90)*scale.x,SXF_SETTARGET,80);
			}
		}
		CPOY O 3 A_PlaySound("gibs/meatland");
		CPOY P 3;
		CPOY Q 3 A_PlaySound("gibs/meatland");
		CPOY RS 3;
		goto deathbleed;
	XDeath:
		TNT1 A 0 A_JumpIf(bdoom_gibs==true,"XDeath.Particles");
	XDeath.Vanilla:
		CGIB ab 2;
		CGIB c 2 a_xscream();
		CGIB de 2;
		CGIB f 2 A_NoBlocking();
		CGIB gh 2;
		CGIB ijK 2;
		goto deathbleed;
	LoadSprites:
		RPOM ABCDEFGHIJKLM 0;
		RPON ABCDEFGHIJK 0;
		RGIB ABCDEFGHIJK 0;
		RPAR A 0;
		RPAS ABCDEF 0;
		RPO1 ABCD 0;
		RPOG AB 0;
		stop;
	}
}
