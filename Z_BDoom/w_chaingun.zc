Class BD_Chaingun : BDoomWeapon {
	double HeatStacks;
	double HeatDecay;
	protected bool RifleSights;
	protected int tfired;
	
	protected int spintics;
	protected double spinspeed;
	protected int spinframe;
	
	protected bool isLocked;
	
	state s_spin;
	
	Default	{
		Tag "$TAG_CHAINGUN";
		weapon.selectionorder 70;
		Weapon.AmmoType		"Clip";
		Weapon.AmmoUse			1;
		Weapon.AmmoGive		0;
		BDoomWeapon.magammo	"RifleClip";
		Weapon.AmmoType2		"Clip";
		Weapon.AmmoUse2		1;
		Weapon.AmmoGive2		20;
		weapon.slotnumber		4;
		BDoomWeapon.AutoReloadTime 140;
		BDoomWeapon.reloadsound "Mweapons/rifle/clipin";
	}
	action void SpawnChaingunCasing() {
		if(bdoom_debris)
			A_Eject("BD_ChaingunCasing",10,frandom[bdsfx](5,7),20,frandom[bdsfx](0,2),frandom[bdsfx](3.5,4.5),frandom[bdsfx](3.2,4));
	}
	action void SpawnRifleCasing() {
		if(!bdoom_debris)
			return;
		if (invoker.RifleSights)
			A_Eject("BD_ChaingunCasing",13,frandom[bdsfx](3,4),4,frandom[bdsfx](0,2),frandom[bdsfx](8,9),frandom[bdsfx](2,2.5));
		else
			A_Eject("BD_ChaingunCasing",20,frandom[bdsfx](1,2),12.5,frandom[bdsfx](0,2),frandom[bdsfx](4.5,5),frandom[bdsfx](2,2.5));
	}
	
	override string PickupMessage () {
		if(bdoom_weapons == 2) 
			return "You got the Assault Rifle!"; 
		else
			return "$GOTCHAINGUN"; 
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		s_spin = FindState("AltHold");
	}
	override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack) {
		if(bdoom_weapons == 2) 
			return "%o was penetrated by %k's assault rifle."; 
		else 
			return "$OB_MPCHAINGUN"; 
	}
	override void Tick() {
		If (Owner)	
			return;
		else {
			if(bdoom_weapons == 2) {
				pickupsound = "Mweapons/rifle/clipin";
			}
			else {
				pickupsound = "pickups/chaingun"; 
			}
		}
		Super.Tick();
	}
	override void DoEffect() {
		Super.DoEffect();
		if (!owner)
			return;
		if(bdoom_weapons == 2) {
			SetTag("Assault Rifle");
			if (RifleSights)
				bNOAUTOFIRE = true;
			else	
				bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = true;
		}
		else {
			SetTag("$TAG_CHAINGUN");
			bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = false;
			//console.printf("spin speed: %d | frame: %d | tics: %d",spinspeed,spinframe,spintics);
		}
		if (bdoom_weapons == 1 && owner.player.readyweapon == self && owner.health > 0) {
			let psp = owner.player.FindPSprite(PSP_Weapon);
			if (!psp)
				return;
			if (heatStacks > 0 && (isLocked || (!(owner.player.cmd.buttons & BT_ATTACK)))) {
				double rd = isLocked ? 0.5 : 1;
				HeatStacks = Clamp(heatStacks-rd,0,60); 
			}
			//handle altfire spinning:
			if (InStateSequence(psp.curstate,s_spin)) {			
				owner.A_StartSound("weapons/chaingun/spin",CHAN_CHGSPIN,flags:CHANF_LOOPING);
				//spinspeed increases while holding altfire
				if (owner.player.cmd.buttons & BT_ALTATTACK && owner.player.oldbuttons & BT_ALTATTACK) {
					spinspeed = Clamp(spinspeed+0.5,0,40);
				}
				//decreases otherwise
				else {
					spinspeed = Clamp(spinspeed-0.5,0,40);
				}
				//spin sound pitch continuously changes based on spinspeed
				owner.A_SoundPitch(CHAN_CHGSPIN,Clamp(spinspeed * 0.022,0.2,1));
				//defines the duration of the current frame based on spinspeed, always between 1-4
				spintics = Clamp(4 - floor(spinspeed * 0.1),1,4);
			}
			//if not in altfire spinspeed is quickly but not immediately decreased and the sound is still played
			else if (spinspeed > 0) {
				spinspeed = Clamp(spinspeed-1,0,40);
				owner.A_SoundPitch(CHAN_CHGSPIN,Clamp(spinspeed * 0.022,0.2,1));
			}
			else
				owner.A_StopSound(CHAN_CHGSPIN);			
			if (heatStacks > 0) {
				owner.A_SoundVolume(CHAN_CHGCOOL,HeatStacks*0.0165);
				let fl = owner.player.FindPsprite(PSP_HEAT);
				if (!fl) {
					owner.player.SetPSprite(PSP_HEAT,FindState("FlashHeated"));
				}
				else
					fl.frame = spinframe;
			}
			else {
				owner.player.SetPSprite(PSP_HEAT,null);
				owner.A_StopSound(CHAN_CHGCOOL);
				isLocked = false;
			}
		}
		else {
			HeatStacks = 0;
			owner.A_StopSound(CHAN_CHGSPIN);
			owner.A_StopSound(CHAN_CHGCOOL);
			isLocked = false;
		}
		
		//crosshair and bob handling for ADS
		if (owner.player.readyweapon == self && bdoom_weapons == 2) {
			let s = owner.player.FindPsprite(PSP_Weapon);
			let chg = BD_Chaingun(owner.player.ReadyWeapon);
			if (!s || !chg)
				return;
			if ((InStateSequence(s.curstate, FindState("ReadySights"))	||	
				InStateSequence(s.curstate, FindState("FireSights"))) 	){
				chg.crosshair = 99; //if ADS used, disable crosshair and set very little bob
				BobRangeX = 0.12;
				BobRangeY = 0.12;
				BobStyle = 5;
				BobSpeed = 2.5;
			}
			else { 
				chg.crosshair = 0; //if ADS isn't used, set crosshair to default
				/*if (bdoom_weapons == 2) { //and set bob to either default Modern value...
					BobRangeX = 0.2;
					BobRangeY = 0.2;
					BobStyle = 5;
					BobSpeed = 2.0;
				}*/
			}
		}
		else { //... or the default non-Modern value
			tfired = 0;						//set spread value to 0
			RifleSights = false;				//unset ADS
			/*BobRangeX = 1.0;
			BobRangeY = 1.0;
			BobStyle = 0;
			BobSpeed = 1.0;*/
		}
	}
	states {
		Spawn:
			MGUN A 1 NoDelay {
				if (bdoom_weapons == 2) {
					sprite = GetSpriteIndex("RIFN");
					frame = 25;
					A_SetScale(0.75);
				}
				else {
					sprite = GetSpriteIndex("MGUN");
					frame = 0;
					A_SetScale(1.0);
				}
			}
			loop;
			RIFN Z 1;
		Select:
			TNT1 A 0 { 
				A_StopSound(CHAN_5);			
				if (bdoom_weapons == 2)
					return ResolveState("Select.Modern");
				If(bdoom_weapons == 1) 
					A_StartSound("weapons/chaingun/draw");
				A_WeaponOffset(-48,86);
				return ResolveState(null);
			}
			MGGG CDABCD 1 {
				A_WeaponOffset(8,-9,WOF_ADD);
				A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			}
			goto ready;
		Deselect:
			TNT1 A 0 { 
				BD_ClearPSprite(PSP_REDDOT);
				A_StopSound(CHAN_5); 
				if (bdoom_weapons == 2)
					return ResolveState("Deselect.Modern");
				speed = default.speed;
				A_ZoomFactor(1.0);
				return ResolveState(null);
			}
			MGGG A 1 A_Lower();
			wait;
		Ready:
			MGGG A 1 {
				if (bdoom_weapons == 2)
					return ResolveState("Ready.Modern");
				speed = default.speed;
				A_ZoomFactor(1.0); 
				BD_ClearPSprite(PSP_REDDOT);
				if (bdoom_weapons == 0)
					A_WeaponReady(WRF_NOSECONDARY);
				else 
					A_WeaponReady();
				return ResolveState(null);
			}
			loop;
		Fire:
			TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Fire.Modern");
			MGGG A 1 {
				//A_WeaponOffset(0.0,35.0,WOF_INTERPOLATE);
				a_gunflash();
				BD_FireBullets(5.6, 0, 1, 5,"weapons/chngun","weapons/chaingun/fire") ;
			}
			MGGG B 1;
			TNT1 A 0 SpawnChaingunCasing();
			MGGG CD 1;
			TNT1 A 0 A_CheckReload();
			
			MGGG A 1 {
				a_gunflash();
				BD_FireBullets(5.6, 0, 1, 5,"weapons/chngun","weapons/chaingun/fire") ;
			}
			MGGG B 1;
			TNT1 A 0 SpawnChaingunCasing();
			MGGG CD 1;
			TNT1 A 0 {
				//jump directly to spinning if AltAttack is pressed as well
				if (bdoom_weapons == 1 && (player.cmd.buttons & BT_ATTACK) && (player.cmd.buttons & BT_ALTATTACK)) {
					A_ClearRefire();
					invoker.spintics = 4;
					invoker.spinspeed = 14;
					invoker.spinframe = 0;
					return ResolveState("AltHold");
				}
				//otherwise do regular refire
				else
					A_ReFire("Fire");
				return ResolveState(null);
			}
			MGGG ABCD 1 {
				A_WeaponReady(WRF_NOSECONDARY);
				//A_WeaponOffset(0.0,-0.75,WOF_ADD);
			}
			MGGG ABCDAABBCCDD 1 A_WeaponReady(WRF_NOSECONDARY);
			goto ready;
		Flash:
			TNT1 A 1 {
				A_Light2();
				if(!invoker.c_muzzle)
					A_Overlay(PSP_EFLASH,"FlashVanilla");
				else {
					for (int i = random (2,3); i > 0; i --)
						A_Eject("BD_MuzzleSmoke",12,0,4,32);
					A_Overlay(PSP_EFLASH,"FlashEnhanced");
					A_Overlay(PSP_MFLASH,"FlashMuzzle");
					if (random(0,1) == 1) 
						A_OverlayFlags(PSP_MFLASH,PSPF_FLIP,true);
				}
				A_OverlayFlags(PSP_EFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayFlags(PSP_MFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayRenderstyle(PSP_EFLASH,STYLE_Add);
				A_OverlayRenderstyle(PSP_MFLASH,STYLE_Add);
			}
			TNT1 A 1 A_Light1();
			goto lightdone;
		FlashVanilla:
			MGGL AB 1 bright;
			goto lightdone;
		FlashEnhanced:
			MGGM A 1 bright;
			MGGM B 1 bright A_OverlayAlpha(PSP_EFLASH,0.8);
			MGGM C 1 bright A_OverlayAlpha(PSP_EFLASH,0.6);
			MGGM D 1 bright A_OverlayAlpha(PSP_EFLASH,0.4);
			goto lightdone;
		FlashMuzzle:
			MUZ2 A 1 bright {
					A_OverlayOffset(PSP_MFLASH,frandom(-0.5,0.5),frandom(-0.5,0.5));
					let fl = player.FindPSprite(OverlayID());
					fl.frame = random(0,3);
				}
			#### # 1 bright A_OverlayAlpha(PSP_MFLASH,0.8);
			stop;
		FlashHeated:
			MGGH # 1 bright {
				A_OverlayRenderstyle(OverlayID(),Style_ADD);
				A_OverlayFlags(OverlayID(),PSPF_Alpha|PSPF_ForceAlpha,true);
				A_OverlayAlpha(OverlayID(),Clamp(invoker.HeatStacks*0.0165,0,1));
				/*let psp = Player.FindPSprite(OverlayID());
				if (psp)
					psp.frame = invoker.spinframe;*/
			}
			loop;
		AltFire:
			TNT1 A 0 {
				if (bdoom_weapons == 2)
					return ResolveState("AltFire.Modern");
				invoker.spintics = 4;
				invoker.spinspeed = 0;
				invoker.spinframe = 0;
				return ResolveState("AltHold");
			}
		AltHold:
			MGGG #### 4 {
				if (bdoom_weapons != 1) {
					invoker.isLocked = false;
					invoker.spinspeed = 0;
					return ResolveState("Ready");
				}
				A_SetTics(invoker.spintics);
				int i = invoker.spinframe;
				i = invoker.spinspeed > 35 ? i+2 : i+1;
				i = Clamp(i,0,4);
				invoker.spinframe = i > 3 ? i-4 : i;
				let psp = Player.FindPSprite(OverlayID());
				if (psp)
					psp.frame = invoker.spinframe;
				if (invoker.isLocked && random[bdsfx](0,60) < invoker.heatStacks) {
					A_Eject("BD_WeaponSmoke",frandom[bdsfx](8,12),frandom[bdsfx](-2.5,2.5),frandom[bdsfx](2,4),zvel:frandom[bdsfx](0.3,0.8));
				}
				if (invoker.ammo1.amount > 0 && !invoker.isLocked && player.cmd.buttons & BT_ATTACK && invoker.spinframe == 0) {
					invoker.HeatStacks = Clamp(invoker.heatStacks+1.2,0,60);
					if (invoker.heatStacks >= 60) {
						invoker.isLocked = true;
						A_StartSound("weapons/chaingun/cool",CHAN_CHGCOOL,flags:CHANF_LOOP);
						A_StartSound("weapons/chaingun/lock");
					}
					A_GunFlash("AltFlash");
					A_StartSound("weapons/chaingun/fire",flags:CHANF_OVERLAP);
					Class<Actor> trac = bdoom_tracers ? "BD_BulletTracer" : "";
					A_FireBullets (8,3, -1, 5, "BD_EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,missile:trac,spawnheight:0) ;
					SpawnChaingunCasing();
				}
				if (! ((player.cmd.buttons & BT_ALTATTACK) && (player.oldbuttons & BT_ALTATTACK)) ) {
					if (invoker.spinspeed <= 0)
						return ResolveState("Ready");	
					else if (invoker.spinspeed <= 16)
						A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB);
				}
				return ResolveState(null);	
			}
			wait;
		AltFlash:
			TNT1 A 1 {
				A_Light2();
				if(!invoker.c_muzzle)
					A_Overlay(PSP_EFLASH,"AltFlashVanilla");
				else {
					for (int i = random (2,3); i > 0; i --)
						A_Eject("BD_MuzzleSmoke",12,0,4,32);
					A_Overlay(PSP_EFLASH,"AltFlashEnhanced");
					A_Overlay(PSP_MFLASH,"AltFlashMuzzle");
					if (random(0,1) == 1) 
						A_OverlayFlags(PSP_MFLASH,PSPF_FLIP,true);
				}
				A_OverlayFlags(PSP_EFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayFlags(PSP_MFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayRenderstyle(PSP_EFLASH,STYLE_Add);
				A_OverlayRenderstyle(PSP_MFLASH,STYLE_Add);
			}
			TNT1 A 1 A_Light1();
			goto lightdone;
		AltFlashVanilla:
			MGGL AC 1 bright;
			goto lightdone;
		AltFlashEnhanced:
			MGGM A 1 bright;
			MGGM C 1 bright A_OverlayAlpha(PSP_EFLASH,0.6);
			goto lightdone;
		AltFlashMuzzle:
			MUZ2 A 1 bright {
					A_OverlayOffset(PSP_MFLASH,frandom(-0.5,0.5),frandom(-0.5,0.5));
					let fl = player.FindPSprite(OverlayID());
					fl.frame = random(0,3);
				}
			#### # 1 bright A_OverlayAlpha(PSP_MFLASH,0.25);
			stop;

		//MODERN
		Select.Modern:
			TNT1 A 0 {
				A_StartSound("MWeapons/rifle/select");
				A_Weaponoffset(30.0,67.0);
				BD_SetSprite("RIFN","QIFN");
			}
			#### AAAAAAA 1 {
				A_WeaponOffset(-4.0,-5.0,WOF_ADD);
				A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			}
			goto ready;
		Deselect.Modern:
			TNT1 A 0 { 
				if (!invoker.RifleSights)
					return ResolveState("Deselect1.Modern");
				speed = default.speed;
				A_ZoomFactor(1.0);
				BD_SetSprite("RIFA","QIFA");
				return ResolveState(null);
			}
			#### CBA 1;
		Deselect1.Modern:
			TNT1 A 0 BD_SetSprite("RIFN","QIFN");
			#### A 1 A_Lower(12);
			wait;
		Ready.Modern:
			RIFN A 1 {
				if (invoker.riflesights)
					return ResolveState("ReadySights");				
				BD_SetSprite("RIFN","QIFN");
				if (CountInv("RifleClip") < 1) {
					let psp = player.FindPsprite(PSP_WEAPON);
					if (psp)
						psp.frame = 4;
				}
				BD_WeaponReady(false);
				if ((player.cmd.buttons & BT_ALTATTACK) && !(player.oldbuttons & BT_ALTATTACK))
					return ResolveState("AltFire.Modern");
				return ResolveState(null);
			}
			goto ready;
		ReadySights:
			RIFS A 1 {
				BD_WeaponReady(false,true,false);
				if ((player.cmd.buttons & BT_ALTATTACK) && !(player.oldbuttons & BT_ALTATTACK))
					return ResolveState("AltFire.Modern");
				speed = default.speed * 0.8;
				A_Overlay(PSP_REDDOT,"Reddot");
				A_OverlayFlags(PSP_REDDOT,PSPF_ADDWEAPON|PSPF_ADDBOB,false);
				A_OverlayFlags(PSP_REDDOT,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_REDDOT,STYLE_Add);
				A_ZoomFactor(1.3);
				return ResolveState(null);
			}
			goto ready;	
		Fire.Modern:
			TNT1 A 0 {
				BD_SetSprite("RIFN","QIFN");
				if (CountInv("RifleClip") < 1)
					return ResolveState("DryReload");
				if (invoker.riflesights)
					return ResolveState("FireSights");
				return ResolveState(null);
			}
			#### A 1 {				
				A_gunflash("FlashModern");
				A_StartSound("Mweapons/rifle/fire");
				A_WeaponOffset(frandom(-0.3,0.3),2,WOF_ADD);
				A_FireBullets(1.5 + (invoker.tfired * 0.15),1.0 + (invoker.tfired * 0.15),-1,0,"BD_NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
				A_SetPitch(pitch - frandom(0.37,0.42),SPF_INTERPOLATE);
				A_ZoomFactor(0.996,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
				invoker.tfired = clamp(invoker.tfired+1,0,9);
			}
			#### B 1 {
				A_WeaponOffset(frandom(-0.3,0.3),4,WOF_ADD);
				A_ZoomFactor(1,ZOOM_NOSCALETURNING);
			}		
			#### CD 1 A_WeaponOffset(0,-3,WOF_ADD);
			#### A 0 {
				SpawnRifleCasing();
				A_ReFire("Fire.Modern");
				if (CountInv("RifleClip") < 1) {
					let s = Player.FindPsprite(PSP_Weapon);
					if (s)
						s.frame = 3;
				}
			}
			#### # 1 {
				A_WeaponOffset(0,32.0,WOF_INTERPOLATE);
				invoker.tfired = 0;
			}
			#### # 2 A_WeaponReady();
			goto ready;
		FireSights:
			RIFS A 1 {
				let plr = PlayerPawn(self);
				plr.AttackZOffset = 13.5;
				A_FireBullets(0.4,0.15,-1,0,"BD_NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
				A_gunflash("FlashModern");
				A_StartSound("Mweapons/rifle/fire");
				A_SetPitch(pitch - 0.3,SPF_INTERPOLATE);
				A_ZoomFactor(1.29,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			}
			#### B 1 {
				A_WeaponOffset(frandom(-1.5,1.5),5,WOF_ADD);
				A_ZoomFactor(1.3,ZOOM_NOSCALETURNING);
				SpawnRifleCasing();
			}
			#### CB 1 A_WeaponOffset(0,-2.5,WOF_ADD);
			#### # 0 {
				A_WeaponOffset(0,32,WOF_INTERPOLATE);
				let plr = PlayerPawn(self);
				plr.AttackZOffset = plr.default.AttackZOffset;
				if (CountInv("RifleClip") < 1)
					return ResolveState("DryReload");
				return ResolveState(null);
			}
			#### A 1 {
				let plr = PlayerPawn(self);
				plr.AttackZOffset = 13.5;
				A_FireBullets(0.4,0.15,-1,0,"BD_NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
				A_gunflash("FlashModern");
				A_StartSound("Mweapons/rifle/fire");
				A_SetPitch(pitch - 0.3,SPF_INTERPOLATE);
				A_ZoomFactor(1.29,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			}
			#### B 1 {
				A_WeaponOffset(frandom(-1.5,1.5),5,WOF_ADD);
				A_ZoomFactor(1.3,ZOOM_NOSCALETURNING);
				SpawnRifleCasing();
			}
			#### CB 1 A_WeaponOffset(0,-2.5,WOF_ADD);
			#### # 0 {
				A_WeaponOffset(0,32,WOF_INTERPOLATE);
				let plr = PlayerPawn(self);
				plr.AttackZOffset = plr.default.AttackZOffset;
				if (CountInv("RifleClip") < 1)
					return ResolveState("DryReload");
				return ResolveState(null);
			}
			#### A 1 {
				let plr = PlayerPawn(self);
				plr.AttackZOffset = 13.5;
				A_FireBullets(0.4,0.15,-1,0,"BD_NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
				A_gunflash("FlashModern");
				A_StartSound("Mweapons/rifle/fire");
				A_SetPitch(pitch - 0.3,SPF_INTERPOLATE);
				A_ZoomFactor(1.29,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			}
			#### B 1 {
				A_WeaponOffset(frandom(-1.5,1.5),5,WOF_ADD);
				A_ZoomFactor(1.3,ZOOM_NOSCALETURNING);
				SpawnRifleCasing();
			}
			#### CB 1 A_WeaponOffset(0,-2.5,WOF_ADD);
			#### A 3 {				
				A_WeaponOffset(0,32,WOF_INTERPOLATE);
				let plr = PlayerPawn(self);
				plr.AttackZOffset = plr.default.AttackZOffset;
				A_WeaponReady(WRF_NOFIRE);
				if (CountInv("RifleClip") < 1)
					return ResolveState("Reload");
				return ResolveState(null);
			}
			goto ready;
		DryReload:
			RIFN A 8 {
				BD_SetSprite("RIFN","QIFN");
				A_ClearRefire();
				A_StartSound("Mweapons/dry",slot:CHAN_7);
				if (invoker.RifleSights) {
					let psp = player.FindPSprite(PSP_Weapon);
					if (psp) 
						psp.sprite = GetSpriteIndex("RIFS");
				}
				else
					BD_SetSprite("RIFN","QIFN");
			}
			goto reload;
		Reload:
			RIFA A 0 { 
				A_ClearRefire();
				BD_ClearPsprite(PSP_REDDOT);
				if (CountINv("Clip") == 0)
					return ResolveState("Ready");
				if (CountInv("RifleClip") < 1)
					invoker.wasempty = true;
				if (!invoker.RifleSights)
					Return ResolveState("ReloadDo");
				else {
					invoker.RifleSights = false;
					speed = default.speed;
					A_ZoomFactor(1.0);
				}
				BD_SetSprite("RIFA","QIFA");
				return ResolveState(null);
			}
			#### CBA 1;
		ReloadDo:
			TNT1 A 0 BD_SetSprite("RIFR","QIFR");
			#### ABCDE 2;
			#### F 8 A_StartSound("Mweapons/rifle/clipout");
			#### GH 2;
			#### I 5;
			#### JKL 2;
			#### M 1 A_StartSound("Mweapons/rifle/clipin");
			#### MMM 1 A_WeaponOffset(1,0,WOF_ADD);
			#### M 1 A_WeaponOffset(-1,0,WOF_ADD);
			#### A 0 {
				if (!invoker.wasempty)
					BD_Reload();
			}
			#### LN 2;
			TNT1 A 0 { 
				if (!invoker.wasempty)
					return ResolveState("ReloadEnd");
				BD_SetSprite("RIFB","QIFB");
				return ResolveState(null);
			}			
			#### ABC 1;
			#### DEF 2;
			#### A 0 A_StartSound("Mweapons/rifle/bolt");
			#### GHIJ 1;
			#### IHG 3;
			#### A 0 BD_Reload();
			#### FED 2;
			#### CBA 1;
		ReloadEnd:
			TNT1 A 0 {
				invoker.wasempty = false;
				BD_SetSprite("RIFR","QIFR");
			}
			#### OEDCBA 2;
			goto ready;
		AltFire.Modern:
			TNT1 A 0 { 
				A_ClearRefire();
				if (invoker.RifleSights)
					return ResolveState("UnsetSights");
				invoker.RifleSights = true; 
				A_StartSound("Mweapons/rifle/aim",slot:CHAN_7);
				speed = default.speed * 0.8;
				A_ZoomFactor(1.3);
				BD_SetSprite("RIFA","QIFA");
				return ResolveState(null);
			}
			#### ABCD 1;
			goto ReadySights;
		UnsetSights:
			TNT1 A 0 { 
				BD_ClearPSprite(PSP_REDDOT);
				invoker.RifleSights = false;
				A_StartSound("Mweapons/rifle/unaim",slot:CHAN_7);
				speed = default.speed;
				A_ZoomFactor(1.0);
				BD_SetSprite("RIFA","QIFA");
			}
			#### DCBA 1;
			goto Ready;
		Reddot:
			RIFO A 18 bright;
			stop;
		FlashModern:
			TNT1 A 1 {
				A_Light2();
				if (invoker.riflesights)
					A_Overlay(PSP_EFLASH,"FlashModernSights");
				else
					A_Overlay(PSP_EFLASH,"FlashModernHighlights");
				
				if (invoker.c_muzzle) {
					for (int i = random (2,3); i > 0; i --) {
						if (invoker.riflesights)
							A_Eject("BD_MuzzleSmoke",12,0,1.5,32);
						else
							A_Eject("BD_MuzzleSmoke",12,0,4,32);
					}
					A_Overlay(PSP_MFLASH,"FlashModernMuzzle");
					if (invoker.riflesights)
						A_OverlayOffset(PSP_MFLASH,0,27);
					else
						A_OverlayOffset(PSP_MFLASH,0,30);
					A_OverlayFlags(PSP_MFLASH,PSPF_ADDWEAPON,false);
				}
						
				A_OverlayFlags(PSP_EFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayFlags(PSP_MFLASH,PSPF_RENDERSTYLE|PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayRenderstyle(PSP_EFLASH,STYLE_Add);
				A_OverlayRenderstyle(PSP_MFLASH,STYLE_Add);
			}
			TNT1 A 1 A_Light1();
			goto lightdone;
			RIFM ABCD 0;
		FlashModernHighlights:
			RIFL AB 1 bright {
				if (invoker.c_muzzle) {
					let fl = player.FindPSprite(OverlayID());
					if (fl) fl.sprite = GetSpriteIndex("RIFM");
				}
			}				
			stop;
		FlashModernSights:
			RIFL CD 1 bright {
				if (invoker.c_muzzle) {
					let fl = player.FindPSprite(OverlayID());
					if (fl) fl.sprite = GetSpriteIndex("RIFM");
				}
			}
			stop;
		FlashModernMuzzle:
			MUZ5 A 1 bright {
					let fl = player.FindPSprite(OverlayID());
					if (fl) {
						if (invoker.riflesights)
							fl.sprite = GetSpriteIndex("MUZ6");
						fl.frame = random(0,5);
					}
				}
			#### # 1 bright A_OverlayAlpha(OverlayID(),0.8);
			stop;
	}
}

Class RifleClip : ammo {
	Default {
		inventory.icon "HMAGZ0";
		inventory.amount 1;
		inventory.maxamount 40;
		ammo.backpackamount 0;
		ammo.backpackmaxamount 40;
		+INVENTORY.IGNORESKILL
	}
}