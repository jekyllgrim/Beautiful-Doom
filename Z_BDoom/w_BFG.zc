//**********
// BFG-9001
//**********


class BD_BFG9000 : BDoomWeapon {
	int wcharge; //charge of the Modern BFG
	int railcounter; //used for Heavy Laser alt attack
	string PrevSpecies;
	private double wzoom;
	private double wzoommul;
	
	private double lightsalpha;
	ModernBFG_PrepOrb preporb;
	Default {
		BDoomWeapon.dontforcebob True;
		Height 20;
		weapon.ammotype2 	"Cell";
		weapon.ammouse2 	1;
		weapon.ammogive2 	0;
		Weapon.AmmoType 	"Cell";
		Weapon.AmmoUse 		40;
		Weapon.AmmoGive 	40;
		+WEAPON.NOAUTOFIRE;
		Inventory.PickupMessage "$GOTBFG9000";
		Tag "$TAG_BFG9000";	
		Weapon.Selectionorder 280;
		inventory.pickupsound "pickups/bfg";
		ProjectileKickback 100;
		weapon.kickback 30;
		decal "ArachnotronPlasmaSplat";
		weapon.slotnumber 7;
	}
	override void DoEffect() {
		Super.DoEffect();
		If (Owner)
			{
			if(bdoom_weapons == 2) {
				AmmoUse1 = 10;
				AmmoUse2 = 0;
				bALT_AMMO_OPTIONAL = true;
			}
			else {
				AmmoUse1 = 40;
				AmmoUse2 = 1;
				bALT_AMMO_OPTIONAL = false;
			}
			//console.printf("Player Species: %s",owner.species);
		}
		if (owner.player.readyweapon != self && preporb)
			preporb.destroy();
	}
	States {
		Spawn:
			BFUG A 1 {
				if (bdoom_weapons == 2)
					scale.x = -1;
				else
					scale.x = 1;
			}
			loop;
		Select:
			TNT1 A 0 {
				If(bdoom_weapons > 0)
					A_PlaySound("weapons/bfg/draw");
				If(bdoom_weapons==2)
					return ResolveState("Select.Modern");
				return ResolveState(null);
			}
			BBGN A 1 A_Raise();
			wait;
		Deselect:
			TNT1 A 0 {
				If(bdoom_weapons==2)
					return ResolveState("Deselect.Modern");
				A_ClearRefire();
				invoker.wcharge = 0;
				invoker.railcounter = 0;
				A_StopSound(5);
				//A_StopSound(6);
				A_StopSound(7);
				return ResolveState(null);
			}
			BBGN A 1 A_Lower();
			wait;
		Ready:
			BBGN A 1 {
				If(bdoom_weapons==2)
					return ResolveState("Ready.Modern");
				string curspecies = species;
				if (invoker.prevspecies && curspecies.IndexOf("BDoomPlayerSpecies") >= 0) //give back original species, if any (and if current is BDoomPlayerSpecies%d)
					species = invoker.prevspecies;
				A_StopSound(5);
				A_StopSound(7);
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
				invoker.wcharge = 0;
				invoker.wzoom = 1.0;
				A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
				if (bdoom_weapons == 1)
					A_WeaponReady();
				else
					A_WeaponReady(WRF_NOSECONDARY);
				return ResolveState(null);
			}
			loop;
		Fire: //before fire = 30 tics, fire = 10 tics, refire = 20 tics
			TNT1 A 0 {
				If(bdoom_weapons==2)
					return ResolveState("Fire.Modern");
				if (bdoom_weapons == 0)
					A_StartSound ("weapons/bfgf",CHAN_WEAPON,flags:CHANF_OVERLAP);
				else
					A_StartSound ("weapons/bfg/start",CHAN_5,flags:CHANF_OVERLAP);
				A_Overlay(PSP_LIGHTS,"LightsOn");
				A_OverlayFlags(PSP_LIGHTS,PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayAlpha(PSP_LIGHTS,0);
				invoker.lightsalpha = 0;
				return ResolveState(null);
			}
			BBGN A 22;
			BBGN A 8 {
				A_Overlay(PSP_FLASH,"Flash");
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			BBGN B 2 {
				A_FireProjectile("BD_BFGBall",0,1);
				if(bdoom_shakes)
					A_Quake(1,20,0,128,"");
				if (bdoom_weapons == 1)
					A_PlaySound("weapons/bfg/fire",5);
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
			}
			BBGN CDEF 2;
			BBGN GGHHIIJJAAAAAAAAAAAA 1 {
				A_ReFire();
				A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
			}
			goto ready;
		LightsOn:
			BBGL ZZZZZZZZZZZZZZZZZZZZ 1 bright {
				invoker.lightsalpha = Clamp(invoker.lightsalpha+0.05,0,1);
				A_OverlayALpha(OverlayID(),invoker.lightsalpha);
			}
		LightsBlink:
			BBGL ZZ 2 bright {
				if (invoker.lightsalpha > 0.65)
					invoker.lightsalpha = 0.65;
				else
					invoker.lightsalpha = 1;				
				A_OverlayALpha(OverlayID(),invoker.lightsalpha);
				let psp = player.FindPSprite(PSP_Weapon);
				if (!psp || player.readyweapon != invoker)
					return ResolveState("Null");
				return ResolveState(null);
			}
			loop;
		Flash:
			BBGF ABCD 2 bright;
		FlashMain:
			BBGF EFGHIJK 2 bright;
			stop;
		AltFire:
			BBGN A 5 {
				A_StopSound(5);
				A_PlaySound("weapons/bfg/railcharge",5,1,0);
			}
			BBGN A 20 {
				A_Overlay(PSP_LIGHTS,"LightsOn");		
				A_OverlayFlags(PSP_LIGHTS,PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayAlpha(PSP_LIGHTS,0);
				invoker.lightsalpha = 0;
			}
			TNT1 A 0 {
				A_PlaySound("weapons/bfg/railfire",CHAN_5);
				A_Overlay(PSP_FLASH,"AltFlash");			
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
		RailFire:
			BBGN A 2 {
				If(invoker.railcounter >= 100)
					return ResolveState("AltEnd");					
				A_WeaponOffset(random(-1,1),random(32,34),WOF_INTERPOLATE);
				A_RailAttack(10*random(1,6),0,1,"","green",RGF_SILENT|RGF_NORANDOMPUFFZ,1,"BFGRailPuff",0,0,300,0,5.0,0,"BFGRailBall_Hold",-4); //this is memory-light
				if (invoker.railcounter >= 30 && bdoom_shakes == 1)
					A_Quake(1,2,0,64,"");
				invoker.railcounter++;
				return ResolveState(null);
			}
			TNT1 A 0 A_ReFire("RailFire");
			goto AltEnd;
		AltEnd:
			TNT1 A 0 {
				A_ClearRefire();
				A_WeaponOffset(0,32);
				A_RailAttack(0,0,0,"","green",RGF_SILENT|RGF_NORANDOMPUFFZ,2,"BFGRailPuff",0,0,300,0,3.0,5.0,"BFGRailBall",-4); //this memory-heavy ray is for nicer after-shot effect similar to plasma railgun
				A_StopSound(CHAN_5);
				A_Playsound("weapons/bfg/railcool",CHAN_6);
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
				A_Overlay(PSP_FLASH,"FlashMain");			
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);
			}
			BBGN BCDEFGHIJAAA 2 {
				if (bdoom_debris)
					A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
			}
			BBGN A 5 {
				If(invoker.railcounter >= 100)
					return ResolveState("AltEndLong");
				invoker.railcounter = 0;
				return ResolveState(null);
			}
			goto ready;	
		AltEndLong: //Played only if you held the button until the end
			BBGN AAAAAAAAAAAAAAAA 1 {
				if (bdoom_debris)
					A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
			}
			BBGN AAAAA 1 { 
				if (bdoom_debris)
					A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
				invoker.railcounter = 0; 
			}
			goto ready;	
		AltFlash:
			BBGG A 1 bright {
				let psp = player.FindPSprite(PSP_Weapon);
				if (!psp || player.readyweapon != invoker)
					return ResolveState("Lightdone");
				let fl = player.FindPSprite(OverlayID());
				if (fl)
					fl.frame = random(0,3);					
				return ResolveState(null);
			}
			loop;
			
	// MODERN
		Select.Modern:
			TNT1 A 0 A_WeaponOffset(-36.0,64.0);
			BBGN AAAAAAAA 1 {
				A_WeaponOffset(4.5,-4.0,WOF_ADD|WOF_INTERPOLATE);
				A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			}
			goto ready;
		Deselect.Modern:
			TNT1 A 0 {
				string curspecies = species;
				if (invoker.prevspecies && curspecies.IndexOf("BDoomPlayerSpecies") >= 0) //give back original species, if any (and if current is BDoomPlayerSpecies%d)
					species = invoker.prevspecies;
				A_ClearRefire();
				invoker.wcharge = 0;
				A_StopSound(5);
				//A_StopSound(6);
				A_StopSound(7);
			}
			BBGN A 1 A_Lower();
			wait;
		Ready.Modern:
			TNT1 A 0 {
				A_ClearRefire();
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
				string curspecies = species;
				if (curspecies.IndexOf("BDoomPlayerSpecies") < 0) {
						invoker.prevspecies = species; //record previous species, if any (and if not already BDoomPlayerSpecies%d)
						species = String.Format("BDoomPlayerSpecies%d",self.PlayerNumber()); //assign unique species based on player number
					}			
				invoker.wcharge = 0;
				invoker.railcounter = 0;
				invoker.wzoom = 1.0;
				A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
				let po = player.findPsprite(PSP_HEAT);
				if (!po && invoker.ammo1.amount > 0) {
					A_OverLay(PSP_HEAT,"ReadyLights");
					A_OverlayFlags(PSP_HEAT,PSPF_RENDERSTYLE,true);
					A_OverlayRenderstyle(PSP_HEAT,Style_normal);
				}
			}
			BBGN A 1 {		
				If(bdoom_weapons!=2)
					return ResolveState("Ready");
				A_WeaponReady(WRF_NOSECONDARY);
				return ResolveState(null);
			}
			wait;
		ReadyLights:
			BMGY ABCDEFEDCB 2 bright;
			loop;
		ChargeLights:
			BMGZ A 1 {
				let po = player.findPsprite(OverlayID());
				po.frame = clamp(invoker.wcharge,0,7);
				return ResolveState(null);
			}
			loop;
			//load:
			BMGZ ABCDEFGH 1;
		OverChargeLights:
			BMGZ KJI 2;
			loop;		
		LightsOn_Modern:
			BBGL ZZZZZZZZZZZZZZZZZZZZZZZZZZZZ 4 bright {
				invoker.lightsalpha = Clamp(invoker.lightsalpha+0.0357,0,1);
				A_OverlayALpha(OverlayID(),invoker.lightsalpha);
			}
			goto LightsBlink;
		Fire.Modern:
			TNT1 A 0 {
				A_StopSound(5);
				A_StopSound(6);
				A_StopSound(7);
				invoker.wcharge = 0;
				//charging lights:
				A_Overlay(PSP_HEAT,"ChargeLights");
				A_Overlay(PSP_LIGHTS,"LightsOn_Modern");		
				A_OverlayFlags(PSP_LIGHTS,PSPF_ALPHA|PSPF_FORCEALPHA,true);
				A_OverlayAlpha(PSP_LIGHTS,0);
				invoker.lightsalpha = 0;
				//flickering highlights:
				A_Overlay(PSP_FLASH,"AltFlash");
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);				
				A_PLaySound("Mweapons/bfg/charge",CHAN_5);
				//spawn rotating orb in front of the barrel
				invoker.preporb = ModernBFG_PrepOrb(Spawn("ModernBFG_PrepOrb",player.mo.pos));
				if (invoker.preporb) {
					invoker.preporb.master = player.mo;
					invoker.preporb.bfg = BD_BFG9000(invoker);
				}
			}
		Hold.Modern:
			BBGN A 14 {
				A_SetTics(11+invoker.wcharge);
				A_TakeInventory("Cell",10);
				if (invoker.wcharge > 8) {
					A_Overlay(PSP_HEAT,"OverChargeLights");
					A_PLaySound("Mweapons/bfg/charge2",CHAN_6);
					A_PLaySound("Mweapons/bfg/warning",CHAN_7);
					return ResolveState("OverCharge");
				}
				invoker.wcharge++;
				If (CountInv("Cell") < 10)
					return ResolveState("FireBFG");
				return ResolveState(null);
			}
			TNT1 A 0 A_ReFire("Hold.Modern");
			goto FireBFG;
		OverCharge:	
			BBGN A 25 {
				if (invoker.wcharge >= 12) {
					return ResolveState("FireSelf");
				}
				invoker.wcharge++;
				return ResolveState(null);
			}
			TNT1 A 0 A_ReFire("OverCharge");
			goto FireBFG;
		FireBFG:
			BBGN A 8 {
				if (invoker.preporb)
					invoker.preporb.destroy();
				A_Overlay(PSP_FLASH,"Flash");
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);		
				A_ClearRefire();
				A_ClearOverlays(PSP_HEAT,PSP_HEAT);
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
			}
			TNT1 A 0 {
				A_StopSound(CHAN_5);
				A_PlaySound("Mweapons/bfg/fire",CHAN_6);
				A_FireProjectile("ModernBFGBall",0,0);
				if(bdoom_shakes)
					A_Quake(0.5*invoker.wcharge,12 + invoker.wcharge,0,128,"");
				invoker.wzoom = 1 - (invoker.wcharge * 0.02);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING|ZOOM_INSTANT);
			}
			BBGN BBCCDDEEFFGGHHIIJJ 1 {
				invoker.wzoom = Clamp ( invoker.wzoom *= 1.005, 0.8, 1);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING);
			}
			TNT1 A 0 {
				A_Overlay(PSP_LIGHTS,"LightsBlink");		
				A_OverlayFlags(PSP_LIGHTS,PSPF_ALPHA|PSPF_FORCEALPHA,true);
			}
			BBGN A 1 {
				invoker.wzoom = Clamp ( invoker.wzoom *= 1.009, 0.8, 1);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING);
				if (invoker.wzoom >= 1) {
					invoker.wcharge = 0;
					A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
					return ResolveState("Ready");
				}
				A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
				return ResolveState(null);
			}
			wait;					
		FireSelf:
			BBGN A 8 {
				if (invoker.preporb)
					invoker.preporb.destroy();
				A_ClearOverlays(PSP_HEAT,PSP_HEAT);
				A_ClearOverlays(PSP_LIGHTS,PSP_LIGHTS);
				A_Overlay(PSP_FLASH,"Flash");			
				A_OverlayFlags(PSP_FLASH,PSPF_RENDERSTYLE,true);
				A_OverlayRenderstyle(PSP_FLASH,STYLE_Add);	
				A_ClearRefire();
			}
			BBGN B 2 {
				A_StopSound(CHAN_6);
				A_StopSound(CHAN_7);
				A_FireProjectile("ModernBFGBall",0,0);
				A_Quake(1,14,0,128,"");
				A_PlaySound("Mweapons/bfg/fire",CHAN_5);
				invoker.wzoom = 1 - (invoker.wcharge * 0.01);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING|ZOOM_INSTANT);
			}
			BBGN CCDDEEFFGGHH 1 {
				invoker.wzoom = Clamp ( invoker.wzoom *= 1.004, 0.8, 1);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING);
			}
			BBGN IIJJ 1 {
				invoker.wzoom = Clamp ( invoker.wzoom *= 1.004, 0.8, 1);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING);
				A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
			}
			TNT1 A 0 {
				A_Overlay(PSP_LIGHTS,"LightsBlink");		
				A_OverlayFlags(PSP_LIGHTS,PSPF_ALPHA|PSPF_FORCEALPHA,true);
			}
			BBGN AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 {
				invoker.wzoom = Clamp ( invoker.wzoom *= 1.004, 0.8, 1);
				A_ZoomFactor(invoker.wzoom,ZOOM_NOSCALETURNING);
				A_Eject("WeaponSmoke",12, frandom(-3,3),frandom(0,6), frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.1,0.4));
			}
			TNT1 A 0 A_ZoomFactor(1,ZOOM_NOSCALETURNING);
			goto ready;
	}
}

Class ModernBFG_PrepOrb : SmallDebris {
	BD_BFG9000 bfg;
	Default {
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+BRIGHT
		renderstyle 'Add';
		alpha 0.9;
		scale 0.02;
		gravity 0;
	}
	override void Tick() {
		super.Tick();
		if (!master || !master.player)
			return;
		Vector3 ofs = master.player.mo.pos+(0,0,master.player.viewz - master.pos.z);
		Vector3 x, y, z;
		[x, y, z] = Matrix4.getaxes(master.player.mo.pitch,master.player.mo.angle,master.player.mo.roll);
		SetOrigin(ofs+x*16-z*3.5,true);
		if (bfg) {
			roll += 5*bfg.wcharge;
			double jitter = 0.1 * bfg.wcharge;
			scale = (default.scale * (1 + jitter));			
		}
	}
	states {
	Spawn:
		COIR ABCDEFGHIJKLMNOPQRSTUV 2 {
			if (!bdoom_debris)
				return;
			if (!bfg)
				return;
			for (int i = 0; i < bfg.wcharge; i++) {/*
				let xoff = frandom(-10,10);
				let yoff = frandom(-10,10);
				let zoff = frandom(-10,10);
				let colshift = random(0,200);
				A_SpawnParticle(Color(0x00+colshift,0xFF,0x00+colshift),SPF_FULLBRIGHT|SPF_NOTIMEFREEZE,lifetime:10,size:0.3,
								xoff:xoff,yoff:yoff,zoff:zoff,
								velx:xoff*-0.1,vely:yoff*-0.1,velz:zoff*-0.1,
								startalphaf:0.8,fadestepf:0,sizestep:0.3
							);*/
				let orb = Spawn("BFGpreorbParticle",pos+(frandom(-10,10),frandom(-10,10),frandom(-10,10)));
				if (orb) {
					orb.target = self;
					orb.master = master;
				}
			}
		}
		loop;
	}
}

Class BFGpreorbParticle : BD_BaseFlare {
	int ftimer;
	Default {
		translation "GreenPlasma";
		alpha 0.01;
		scale 0.035;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		scale *= frandom(0.8,1.2);
		ftimer = 10;
		moving = true;
	}
	override void Tick() {
		if (!target) {
			destroy();
			return;
		}
		if (isFrozen())
			return;
		double tdist = Distance3D(target);
		A_FaceTarget(0,0,flags:FAF_MIDDLE);
		double fspeed = tdist / ftimer;
		Vel3DFromAngle(fspeed,angle,pitch);
		if (ftimer > 1)
			ftimer--;
		else {
			destroy();
			return;
		}
		super.Tick();
	}
	states {
	Spawn:
		PLBS A 1 A_FadeIn(0.1,FTF_CLAMP);
		loop;
	}
}

Class BD_BFGBall : BFGBall {
	Class<Actor> trailactor;	
	bool dead;
	Default {	
		damagetype "BFGSplash";
		+BLOODLESSIMPACT
		+FORCEXYBILLBOARD
		renderstyle "ADD";
		alpha 0.7;
		decal "none";
		seesound "none";
		DeathSound "none";
		scale 0.4;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		trailactor = "BFGTrail";
	}
	override void Tick () {
		Vector3 oldPos = self.pos;
		Super.Tick();
		if (!bdoom_debris || isFrozen())
			return;
		Vector3 path = level.vec3Diff( self.pos, oldPos );
		double distance = path.length() / 3; //this determines how far apart the particles are
		Vector3 direction = path / distance;
		int steps = int( distance );
		
		for( int i = 0; i < steps; i++ )  {
			actor mo = Spawn(trailactor, oldPos );
			if (mo) {
				//mo.vel = (frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
				mo.target = self;
				mo.angle = angle;
				oldPos = level.vec3Offset( oldPos, direction );
			}
		}
		if (Getclassname() == "BD_BFGBall") {
			if (pos.z <= floorz+100 && floorpic != skyflatnum && random(0,32) > 28) {
				let lt = BFGLightningSpawner(Spawn("BFGLightningSpawner",pos));
				if (lt) {
					lt.master = self;
				}
			}
			if (pos.z >= ceilingz-128 && ceilingpic != skyflatnum && random(0,32) > 28) {
				let lt = BFGLightningSpawner(Spawn("BFGLightningSpawner",pos));
				if (lt) {
					lt.master = self;
					lt.upward = true;
				}
			}
		}
	}
	States {
	Spawn:
		TNT1 A 0 NoDelay {
			A_PlaySound("weapons/bfg/fly",6,1.0,TRUE,1);
			if(bdoom_debris) {
				let f = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
				f.fcolor = 'green';
				f.vel = vel;
				f.master = self;
				f.scale = scale;
			}
		}
	Spawn1:
		BFGB ABCDEFGH 1 {
			A_FaceMovementDirection();
			//if (bdoom_debris)
				//A_CustomRailGun(0,0,"","FFFFFF",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,0,4,"NullPuff",random(0,360),random(0,360),64,20,0,4,"BFGBallRay");
		}
		loop;
	Death.Sky:
		TNT1 A 1 A_RemoveChildren(1,RMVF_EVERYTHING);
		stop;
	Death:
		TNT1 A 0 {
			A_RemoveChildren(1,RMVF_EVERYTHING);
			A_StopSound(6);
			A_SprayDecal("BFGLightning",radius+32);
			A_SetScale(1.0,1.0);
			If(bdoom_shakes  == 0)
				A_Quake(4,34,0,256,"");
			If(bdoom_weapons==1)
				A_PlaySound("weapons/bfg/explosion",6);
			else
				A_PlaySound("weapons/bfgx",6);
			
			/*for (int i = random(56,80); i > 0; i--)
				A_SpawnParticle("51fb45",SPF_FULLBRIGHT|SPF_RELVEL|SPF_RELACCEL|SPF_RELANG|SPF_RELPOS,48,4,random(0,359), 0,0,0, frandom(2,5),0,frandom(-3,3), -0.08,0,randompick(-0.08,0.08));*/
			for (int i = random(8,12); i > 0; i--)
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(96,128),duration:56,sparsity:1.0,spawnclass:"MBFGRailParticle");
		}
		BFGX ABCD 4 {
			dead = true;
			if (bdoom_debris) {
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(96,128),duration:56,sparsity:1.0,spawnclass:"BFGBallRay");
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(96,128),duration:56,sparsity:1.0,spawnclass:"BFGBallRay");
			}
		}
		BFGX E 4 A_BFGSpray("BD_BFGExtra");
		BFGX FGHIJK 4 {
			if (bdoom_debris) {		
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(96,128),duration:56,sparsity:1.0,spawnclass:"BFGBallRay");
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(96,128),duration:56,sparsity:1.0,spawnclass:"BFGBallRay");
			}
		}
		stop;
	}
}

//flare-like trail behind the orb
Class BFGTrail : BD_BaseFlare {
	default {
		alpha 0.2;
		scale 0.16;
	}
	States {	
		Spawn:
			LENG A 1 {
				A_FadeOut(0.08);
				scale*=0.95;
			}
			loop;
	}
}

Class BFGLightningSpawner : SmallDebris {
	bool upward;
	double broll;
	Default {
		+NOINTERACTION
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (master) {
			Warp(master,-52);
			vel = master.vel;
			moving = true;
		}
		broll = frandom(1,2.5);
		angle = random(0,359);
		pitch = frandom(78,89);
		if (upward) {
			broll*=-1;
			pitch*=-1;
		}
	}
	override void Tick() {
		super.Tick();
		if (isFrozen())
			return;
		pitch -= broll;
		if (bdoom_debris)
			A_CustomRailGun(0,0,"","FFFFFF",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:4,pufftype:"NullPuff",range:200,sparsity:0,driftspeed:4,spawnclass:"BFGBallRay");
		if (GetAge() > 20) {
			destroy();
			return;
		}
	}
}

//particles of the decorative lightning of the orb
Class BFGBallRay : BD_BaseFlare {
	Default {
		alpha 0.5;
		scale 0.015;
		translation "GreenPlasma";
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (target) {
			vel = target.vel;
			moving = true;
		}
	}
	States {
	Spawn:
		PEXP ABCDEFGHIJKLM 1 A_FadeOut(0.08);
		stop;
	}
}

//the class that spawns on the targets hit by BFG ball tracers
Class BD_BFGExtra : BFGExtra {
	Default {
		translation "GreenPlasma";
		damagetype "BFGSplash";
		scale 0.3;
		renderstyle "Add";
		alpha 0.6;
	}
	States {
		Spawn:
			TNT1 A 0 NoDelay {
				If(bdoom_debris) {
					for (int i = 24; i > 0; i--)
						A_SpawnItemEx("BFGExtraParticle",frandom(-10,10),frandom(-10,10),frandom(-2,10),frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(0,0.5),failchance:64);
				}
			}
			PEXP AABBCCDDEEFFGGHHIIJJKKLLMM 1 A_FadeOut(0.005);
			stop;
	}
}

Class BFGExtraParticle : BD_BaseFlare {
	Default {
		scale 0.07;
	}
	States {
		Spawn:
			SPRK B 1 {
				A_FadeOut(0.004);
				scale*=0.96;
			}
			wait;
	}
}

//small particles spawned by BFG hits
Class BFGPlasmaPiece : BD_BaseFlare {
	Default {
		translation "GreenPlasma";
		alpha 1.0;
		scale 0.1;		
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		scale *= frandom(0.8,1.2);
	}
	states {
		Spawn:
			PLBS A 1 bright {
				A_FadeOut(0.03);
				vel *= 0.95;
			}
			loop;
	}
}


// particles for heavy laser (alt attack)
Class BFGRailBall : BD_BaseFlare {
	Default {
		renderstyle "Add";
		alpha 0.6;
		scale 0.17;
		translation "GreenPlasma";
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (target && distance3D(target) < 56)
			self.destroy();
	}
	States {
		Spawn:
			TNT1 A 0 NoDelay {
				if(bdoom_debris)
					A_SpawnItemEx("BFGRailFlare",0,0,0,0,0,0,0);
			}
			PEXP ABCDEFGHIJKLM 1 A_FadeOut(0.1);
			stop;
	}
}

//flares for heavy laser particles
Class BFGRailFlare : BD_BaseFlare {
	Default {
		alpha 0.05;
		scale 0.2;
	}
	States {
		Spawn:
			LENG A 1 A_FadeOut(0.0025);
			loop;
	}
}

//bigger particles used on the last heavy laser rail when the user stops shooting (too obnoxious to use all the time)
Class BFGRailBall_Hold : BFGRailBall {
	States {
		Spawn:
			TNT1 A 0 noDelay {
				if(bdoom_debris)
					A_SpawnItemEx("BFGRailFlare_Hold");
			}
			TNT1 A 0 A_Jump(256,1,2,3,4,5);
			PEXP ABCDEFGHIJKLM 1 A_FadeOut(0.2);
			stop;
	}
}

//flares for bigger heavy laser particles
Class BFGRailFlare_Hold : BFGRailFlare {
	States {
		Spawn:
			LENG A 1 A_FadeOut(0.01);
			loop;
	}
}

//heavy laser puff
Class BFGRailPuff : BDoomPuff {
	Default {
		+BLOODLESSIMPACT
		+ALWAYSPUFF
		+NOGRAVITY
		damagetype "BFGSplash";
	}
	States {
		Spawn:
			TNT1 A 1 NoDelay {
				if (bdoom_debris) {
					for (int i = 12; i > 0; i--)
						A_SpawnItemEx("BFGPlasmaPiece",0,0,0,frandom(-2,2),frandom(-2,2),frandom(-2,2),random(0,360));
					/*for (int i = 8; i > 0; i--)
						A_SpawnItemEx("BFGPlasmaShred",0,0,0,randompick(-5,5),randompick(-5,5),randompick(-5,5),random(0,360));*/
					A_CustomRailGun(0,0,"","baffb5",RGF_SILENT|RGF_NOPIERCING|RGF_CENTERZ|RGF_FULLBRIGHT,maxdiff:15,pufftype:"NullPuff",random(0,360),random(0,360),range:32,duration:16,sparsity:0.2,driftspeed:0.1);
					A_CustomRailGun(0,0,"","baffb5",RGF_SILENT|RGF_NOPIERCING|RGF_CENTERZ|RGF_FULLBRIGHT,maxdiff:15,pufftype:"NullPuff",random(0,360),random(0,360),range:32,duration:16,sparsity:0.2,driftspeed:0.1);
				}
			}
			stop;
	}
}


Class ModernBFGBall : BD_BFGBall {
	//wcharge just transfers the charge value from the weapon. 1-8 is normal shot, 9-12 is overcharge (12 leads to self-destruct)
	private double wcharge;
	/*wpower determines power, speed, range and max number of beam targets. Equal to wcharge but clamped to 8 
	 (because overcharging isn't meant to increase power, it just lets you hold off the shot)*/
	private double wpower;
	private double rdistance; 	//effective distance, depends on charge
	private double broll;		//roll change
	private int maxcapacity; 	//max # of targets
		
	private Array < Actor > bfglist;
	private state sspawn;
	
	Default {
		+ROLLSPRITE
		+DONTTHRUST
		speed 32;
		renderstyle "Add";
		scale 0.15;
		alpha 0.9;
		decal "none";
		damage 0;
		radius 14;
		height 8;
	}
	Override void PostBeginPlay() {
		super.PostBeginPlay();
		if (target) {
			species = target.species;
			let BFG = BD_BFG9000 (target.player.readyweapon);
			if (BFG) {
				wcharge = BFG.wcharge;
				wpower = clamp(wcharge,1,8);
				A_SetScale (0.06 + wpower*wpower*0.008);
			}
		}
		broll = 1.0;
		maxcapacity = 6 + wpower;
		rdistance = 256*clamp(wpower,1,5);
		trailactor = "MBFGTrail";
		if (!(vel == (0,0,0)))
			vel = vel.unit() * (5+wpower*2);
		sspawn = FindState("Spawn");
		//console.printf("capacity: %d",maxcapacity);
	}
	Override void Tick() {
		super.Tick();
		if (isFrozen())
			return;
		//Console.printf("%f",scale.x);
		roll+=broll;
		broll*=1.03;
		vel*= 0.98;
		if (!InStateSequence(curstate,sspawn))
			return;
		if (vel.length() < 2) {
			A_SetScale(Clamp(scale.x*1.02,0.1,8));
			alpha*= 1.01;
			if (bdoom_debris && frandom(0.1,2) > vel.length())
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(1,8)+300*scale.x,duration:56,sparsity:1.0,spawnclass:"MBFGRailParticle");
			if (bdoom_debris && frandom(0.1,2) > vel.length())							
				A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(1,8)+300*scale.x,duration:56,sparsity:1.0,spawnclass:"MBFGRailParticle");
		}
		if (bfglist.size() > 0) {
			for (int i = bfglist.size()-1; i >= 0; i--) {
				if (bfglist[i] && (Distance3D(bfglist[i]) > rdistance))
					bfglist.delete(i);
			}
		}
		double closestDist = double.infinity;
		if (bfglist.size() < maxcapacity) {
			BlockThingsIterator itr = BlockThingsIterator.Create(self,rdistance);
			while (itr.next()) {
				let next = itr.thing;
				if (next == target)
					continue; 
				if (!next.bShootable || (!next.bIsMonster && !(next is "PlayerPawn")) || !target.IsHostile (next) || target.bKILLED)
					continue;
				double dist = Distance3D(next);
				if (dist > rdistance)
					continue;
				if (dist < closestDist)
					closestDist = dist;
				tracer = next;
				//Console.Printf("%s",tracer.Getclassname());
				A_Face(tracer,0,0,flags:FAF_MIDDLE);
				if (!CheckLOF (flags: CLOFF_NoAim | CLOFF_SKIPOBSTACLES, ptr_target: AAPTR_tracer))
					continue;		
				if (tracer) {
					bfglist.push(tracer);
					tracer = null;
				}
			}
		}
		if (bfglist.size() > 0) {
			for (int i = bfglist.size()-1; i >= 0; i--) {
				A_Face(bfglist[i],0,0,flags:FAF_MIDDLE);
				A_CustomRailGun(0,0,"","51fb45",RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_CENTERZ|RGF_SILENT ,maxdiff:1,pufftype:"NullPuff",range:rdistance,duration:1,sparsity:0.8,driftspeed:0.1);
				if (!bfglist[i].FindInventory("MBFGTetherControl"))
					bfglist[i].GiveInventory("MBFGTetherControl",1);
				let tether = bfglist[i].FindInventory("MBFGTetherControl");
				if (tether)
					tether.tracer = self;
				master = bfglist[i];
				if (!CheckLOF (flags: CLOFF_NoAim | CLOFF_SKIPOBSTACLES, ptr_target: AAPTR_Master)) {
					bfglist[i].TakeInventory("MBFGTetherControl",1);
					bfglist.delete(i);					
				}
				master = null;
			}
		}
	}
	States {
	Spawn:
		TNT1 A 0 NoDelay {
			if (wcharge >= 12)
				return ResolveState("SelfDestruct");
			A_PlaySound("weapons/bfg/fly",CHAN_6,0.6,TRUE,attenuation:3);
			A_PlaySound("Mweapons/bfg/orbcharge",CHAN_7);
			return ResolveState(null);
		}
	SpawnDo:
		COIR ABCDEFGHIJKLMNOPQRSTUV 2 {
			A_FaceMovementDirection();
			if (vel.length() < 0.5) {
				SetStateLabel("Death");
				A_KillChildren();
			}
		}
		loop;
	Death.Sky:
		TNT1 A 1 A_RemoveChildren(1,RMVF_EVERYTHING);
		stop;
	Death:
		COIX A 1; //makes sure PostBeginPlay has time to execute in case of a point-blank shot
		TNT1 A 0 {
			dead = true;
			if (bdoom_debris) {
				for (int i = 3*wpower; i > 0; i--)
					A_CustomRailGun(0,0,"","baffb5",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,aim:0,maxdiff:15,pufftype:"NullPuff",spread_xy:random(0,360),spread_z:random(0,360),range:random(1,8)+300*scale.x,duration:56,sparsity:1.0,spawnclass:"MBFGRailParticle");
			}
			A_RemoveChildren(1,RMVF_EVERYTHING);
			A_StopSound(CHAN_6);
			A_StopSound(CHAN_7);
			A_Quake(wpower,10*wpower,0,256*wpower,"");
			if (wcharge >= 12)
				return ResolveState("SelfDestruct");
			A_PlaySound("weapons/bfg/explosion",6);
			A_SprayDecal("BFGLightning",radius+32);
			A_Explode(50+60*wpower,64+28*wpower,XF_NOSPLASH,alert:true,fulldamagedistance:64+20*wpower,damagetype:"BFGSplash");
			return ResolveState(null);
		}
		COIX A 10 {
			//console.printf("bfg list size: %d, capacity: %d",bfglist.size(),maxcapacity);
			//this block is intended for point-blank shots when the orb immediately enters Death state and has no time to find targets:
			if (bfglist.size() < maxcapacity) {
				double closestDist = double.infinity;
				for (int i = maxcapacity; i > 0; i--) {
					BlockThingsIterator itr = BlockThingsIterator.Create(self,rdistance);
					while (itr.next()) {
						let next = itr.thing;
						if (next == target)
							continue; 
						if (!next.bShootable || (!next.bIsMonster && !(next is "PlayerPawn")) || !target.IsHostile (next) || target.bKILLED)
							continue;
						double dist = Distance3D(next);
						if (dist > rdistance)
							continue;
						if (dist < closestDist)
							closestDist = dist;
						tracer = next;
						A_Face(tracer,0,0,flags:FAF_MIDDLE);
						if (!CheckLOF (flags: CLOFF_NoAim | CLOFF_SKIPOBSTACLES, ptr_target: AAPTR_tracer))
							continue;		
						if (tracer) {
							//console.printf("%d Found %s",i,tracer.Getclassname());
							bfglist.push(tracer);
							tracer = null;
						}
					}
				}
			}
			if (bfglist.size() > 0) {
				for (int i = bfglist.size()-1; i >= 0; i--) {
					A_Face(bfglist[i],0,0,flags:FAF_MIDDLE);
					A_CustomRailGun(30 + 22*wpower,0,"","51fb45",RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_CENTERZ|RGF_SILENT ,maxdiff:10,pufftype:"MBFGRailPuff",range:rdistance,duration:random(100,150),sparsity:0.15,driftspeed:0.3);
				}
			}
		}
		TNT1 A 0 {
			let ring = Spawn("ModernBFGBall_XRing",pos);
			if (ring) {
				ring.pitch = frandom(15,20)*randompick(-1,1);
				ring.angle = random(0,360);
				ring.master = self;
				ring.scale = scale*1.1;
			}
			let ring2 = Spawn("ModernBFGBall_XRing",pos);
			if (ring2) {
				ring2.pitch = frandom(70,85)*randompick(-1,1);
				ring2.angle = random(0,360);
				ring2.master = self;
				ring2.scale = scale*1.1;
			}
		}
		COIX ABCDEFGHIJKLM 1 { 
			scale *= 1.2;
			alpha *= 0.75;
		}
		stop;				

	SelfDestruct:
		TNT1 A 0 {
			A_Stop();
			A_SetScale(1.0);
			A_PlaySound("Mweapons/bfg/overcharge",6);
			A_Explode(10*wpower,250,flags:XF_HURTSOURCE|XF_NOSPLASH,alert:true,fulldamagedistance:250,damagetype:"BFGSplash");
		}
		COIX ABCDEFGHIJKLM 2 ;//A_CustomRailGun(0,0,"","FFFFFF",RGF_CENTERZ|RGF_SILENT|RGF_NOPIERCING,0,15,"NullPuff",random(0,360),random(0,360),random(22,56),56,3.0,0,"MBFGRailParticle");
		stop;
	}
}

Class ModernBFGBall_XRing : Actor {
	protected double wroll;
	protected double scalemul;
	Default {
		+NOINTERACTION
		+FLATSPRITE
		+ROLLSPRITE
		+BRIGHT
		renderstyle 'Add';
		alpha 0.8;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		wroll = frandom(5,10) * randompick(-1,1);
		roll = random(0,360);
		scalemul = 0.1;
	}
	override void Tick() {
		super.Tick();
		if (isFrozen())
			return;
		if (master) {
			SetOrigin(master.pos,true);
		}
		A_SetRoll(roll+wroll,SPF_INTERPOLATE);
		wroll *= 2;
		scale *= (1 + scalemul);
		scalemul *= 0.95;
	}
	States {
	Spawn:
		COIG ABCDEFGHIJKLMN 1;
		COIG OPQR 1 A_FadeOut(0.2);
		stop;
	}
}

Class MBFGTetherControl : Inventory {
	MBFGTether tether;
	Default {
		inventory.maxamount 1;
	}
	override void Tick() {}
	override void DoEffect() {
		super.DoEffect();
		if (!owner || owner.bKILLED || !tracer) {
			DepleteOrDestroy();
			return;
		}
	}
	override void AttachToOwner(actor other) {
		super.AttachToOwner(other);
		if (owner) {
			tether = MBFGTether(Spawn("MBFGTether",owner.pos+(0,0,owner.height*0.35)));
			tether.master = owner;
		}
	}
	override void DetachFromOwner() {
		if (tether)
			tether.destroy();
		super.DetachFromOwner();
	}
}
	
Class MBFGTether : BD_BaseFlare {
	Default {
		BD_BaseDebris.sfxtype 'debris';
		alpha 0.75;
		scale 0.16;
	}
	override void Tick() {
		super.Tick();
		if (master)
			SetOrigin(master.pos+(0,0,master.height*0.35),true);
		else {
			destroy();
			return;
		}
	}
	states {
	Spawn:
		BFLI ABCDEFGHIJKLMNO 1;
		loop;
	}
}

Class MBFGTrail : BD_BaseFlare {
	Default {
		alpha 0.6;
		+ROLLSPRITE
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (!target)
			return;
		scale = target.scale*0.9;
		roll = target.roll;
		SetOrigin((pos.x+frandom(-2.5,2.5),pos.y+frandom(-2.5,2.5),pos.z+frandom(-2.5,2.5)),false);
	}
	States {	
		Spawn:
			COIR ABCDEFGHIJKLMNOPQRSTUV 1 {
				A_FadeOut(0.05);
				scale*=0.95;
			}
			stop;
	}
}

Class MBFGRailPuff : BFGRailPuff {
	Default {
		+ALLOWTHRUFLAGS
		+MTHRUSPECIES //this will make the rails pass through the player who shot the projectile
		+PUFFGETSOWNER
		+NODECAL
		+NODAMAGETHRUST
	}
}

// smooth particle for the lightning-like purely visual rail that shoots in random directions
Class MBFGRailParticle : BD_BaseFlare {
	BD_BFGBall orb;
	Default {
		scale 0.07;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (target && (target is "BD_BFGBall"))
			orb = BD_BFGBall(target);
	}
	States {
	Spawn:
		SPRK B 1 {
			A_FadeOut(0.002);
			scale*=0.98;
			if (target) {
				if (orb && orb.dead) {
					vel = (frandom(-5,5),frandom(-5,5),frandom(-5,5));
					target = null;
					moving = true;
				}
				else
					vel = target.vel;
			}
			else
				vel *= 0.9;
		}
		wait;
	}
}
	

