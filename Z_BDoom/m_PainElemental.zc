Class BD_PainElemental : BDoomMonster {
	Default	{
		species "PainElemental";
		Health 400;
		Radius 31;
		Height 56;
		Mass 400;
		Speed 8;
		PainChance 128;
		Monster;
		+FLOAT 
		+NOGRAVITY
		SeeSound "pain/sight";
		PainSound "pain/pain";
		DeathSound "pain/death";
		ActiveSound "pain/active";
		Tag "$FN_PAIN";		
		+FORCEXYBILLBOARD
		+FLOATBOB
		FloatBobStrength 0.35;
	}
	override void Tick() {
		super.Tick();
		if (!InStateSequence(curstate,FindState("Spawn")))
			bFLOATBOB = false;
	}
	states {
	Death.Fire:
		stop;
	Death.Greenfire:
		stop;
	Death.PlasmaDamage:
		stop;
	Spawn:
		PAI1 AAAAA 10 A_Look;
		PAI1 B 5 A_Look;
		PAI1 C 5;
		PAI1 D 5 A_Look;
		PAI1 B 5;
		Loop;
	see:
		SAIN abcdedcb 3 a_chase;
		Loop;
	Missile:
		PATT abc 3 a_facetarget;
		PATT d 3 a_facetarget;
		PATT e 2 a_facetarget;
		PATT e 1 a_painattack;
		Goto See;
	Pain:
		
		PPPP a 4;
		PPPP b 2;
		PPPP b 2 a_pain;
		PPPP c 4 a_pain;
		Goto See;
	death:
		TNT1 A 0 {
			scale.x *= randompick[bdsfx](-1,1); 
			gravity = 0.05;
		}
		SPAI AB 4 {
			if (bdoom_blood)
				A_SpawnItemEx("Blood",frandom(-14,14),frandom(-14,14),frandom(12,36),	frandom(-4,4),frandom(-4,4),frandom(3,5),0,SXF_SETTARGET);
		}
		spai c 4 {
			a_scream();
			if (bdoom_blood)
				A_SpawnItemEx("Blood",frandom(-14,14),frandom(-14,14),frandom(12,36),	frandom(-4,4),frandom(-4,4),frandom(3,5),0,SXF_SETTARGET);
		}
		spai def 4 {
			if (bdoom_blood)
				A_SpawnItemEx("Blood",frandom(-14,14),frandom(-14,14),frandom(12,36),	frandom(-4,4),frandom(-4,4),frandom(3,5),0,SXF_SETTARGET);
		}
		TNT1 A 0 {
			A_SetRenderstyle(0.8,Style_ADD);
			if (bdoom_gibs) {
				A_StartSound("gibs/gibbed");
				for (int i = 8; i > 0; i--)
					A_SpawnItemEx("BD_BFountainSpray",frandom(-5.0,5.0),frandom(-5.0,5.0),random(8,28),vel.x*frandom(0.05,0.15),vel.y*frandom(0.05,0.15),frandom(4.5,6.5),0,SXF_ABSOLUTEVELOCITY | SXF_SETTARGET | SXF_TRANSFERTRANSLATION,36);
				A_SpawnItemEx("BD_PainElementalHorn",	frandom(-4,4),frandom(-16,-18),frandom(32,36),	frandom(3,5),0,frandom(4,7),frandom(85,95),		SXF_SETTARGET,32);
				A_SpawnItemEx("BD_PainElementalHorn",	frandom(-4,4),frandom(16,18),frandom(32,36),	frandom(3,5),0,frandom(4,7),frandom(-85,-95),	SXF_SETTARGET,32);
				A_SpawnItemEx("BD_PainElementalJaw",	frandom(-4,4),frandom(-4,4),frandom(6,12),		frandom(-5,5),frandom(-5,5),frandom(3,5),0, 	SXF_SETTARGET,64);
				A_SpawnItemEx("BD_PainElementalJaw",	frandom(-4,4),frandom(-4,4),frandom(6,12),		frandom(-5,5),frandom(-5,5),frandom(3,5),0, 	SXF_SETTARGET,64);
				A_SpawnItemEx("BD_PainElementalHand",	frandom(-4,4),frandom(-20,-22),frandom(20,22),	frandom(3,4),0,frandom(4,7),frandom(85,95),		SXF_SETTARGET,32);
				A_SpawnItemEx("BD_PainElementalHand",	frandom(-4,4),frandom(20,22),frandom(20,22),	frandom(3,4),0,frandom(4,7),frandom(-85,-95),	SXF_SETTARGET,32);
				for (int i = 0; i < 14; i++) {
					A_SpawnItemEx("BD_BasicMeatPiece",	frandom(-10,10),frandom(-10,10),frandom(6,40),	frandom(-5,5),frandom(-5,5),frandom(3,6),0, 	SXF_SETTARGET,32);
					A_SpawnItemEx("BD_BasicMeatPiece_Heavy",	frandom(-10,10),frandom(-10,10),frandom(6,40),	frandom(-5,5),frandom(-5,5),frandom(3,6),0, 	SXF_SETTARGET,160);
				}
				for (int i = 0; i < 7; i++)
					A_SpawnItemEx("LS_FlameShard",	frandom(-6,6),frandom(-6,6),frandom(10,38),		frandom(-7,7),frandom(-7,7),frandom(2,6),0, 	SXF_SETTARGET,32);				
				A_SpawnItemEx("BD_PainExplosion",frandom(-4,4),frandom(-4,4),frandom(14,30),	frandom(-1,1),frandom(-1,1),frandom(-1,1));
			}
		}
		spai g 4 {
			if (bdoom_gibs)
				A_SpawnItemEx("BD_PainExplosion",frandom(-4,4),frandom(-4,4),frandom(14,30),	frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		SPAI H 3 bright {
			if (bdoom_gibs)
				A_SpawnItemEx("BD_PainExplosion",frandom(-4,4),frandom(-4,4),frandom(14,30),	frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		spai i 3 bright a_paindie;
		spai jk 3 bright;
		stop;
	Raise:
		stop;
	XDeath:
		stop;
	}
}

Class BD_PainExplosion : BD_BaseFlare{
	Default {
		+ROLLSPRITE
		alpha 0.75;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		roll = random(0,359);
		A_Setscale(frandom(0.28,0.35));
	}
	states {
	Spawn:
		BOM1 ABCDEFGHIJKLMNOPQRSTUVWX 1 bright {
			roll+10;
			A_FadeOut(0.01);
		}
		stop;
	}
}