Class BD_CyberDemon : BDoomMonster {
	Default	{
		species "Cyberdemon";
		Health 4000;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 16;
		PainChance 20;
		Monster;
		MinMissileChance 160;
		+BOSS 
		+MISSILEMORE
		+FLOORCLIP
		+NORADIUSDMG
		+DONTMORPH
		+BOSSDEATH
		SeeSound "cyber/sight";
		PainSound "cyber/pain";
		DeathSound "cyber/death";
		ActiveSound "cyber/active";
		Obituary "$OB_CYBORG";
		Tag "$FN_CYBER";
	}
	void FireCyberRocket() {
		if (bdoom_replaceweapons && bdoom_weapons == BD_WeaponSet.WS_MODERN) {
			let rock = A_SpawnProjectile("BD_ModernRocket",56,-24);
			if (rock) {
				rock.vel = rock.vel.unit() * 16;
				rock.bFORCERADIUSDMG = false;
			}
		}
		else if (bdoom_projectiles > 0)
			A_SpawnProjectile("BD_Rocket",56,-24);
		else 
			A_SpawnProjectile("Rocket",56);
	}
	states	{
	Raise:
		stop;
	Death.Fire:
		stop;
	Death.Greenfire:
		stop;
	Death.PlasmaDamage:
		stop;
	Death.BFGSplash:
		stop;
	Spawn:
		CBDM A 10 A_Look;
		loop;
	see:
		CBDM a 3 a_hoof;
		CBDM bcdef 3 a_chase;
		CBDM g 3 a_metal;
		CBDM h 3 a_chase;
		loop;
		
	Missile:
		CBDS abc 2 a_facetarget;
		CBDS d 3 FireCyberRocket();
		CBDS e 3;
		CBDS c 6;
		CBDS abc 4 a_facetarget;
		CBDS d 3 FireCyberRocket();
		CBDS e 3;
		CBDS c 6;
		CBDS abc 4 a_facetarget;
		CBDS d 3 FireCyberRocket();
		CBDS e 3;
		CBDS c 6;
		goto see;
	pain:
		CBDI a 4 a_pain;
		CBDI bc 3;
		goto see;
	
	Death:
		TNT1 A 0 {
			if (bdoom_gibs && !bdoom_nobossgibs)
				return ResolveState("Death.Particles");
			return ResolveState(null);
		}
		CBDD a 13;
		CBDD b 5 a_scream;
		CBDD cdefghij 4;
		CBDD k 4 a_noblocking;
		CBDD l 30;
		CBDD l -1 a_bossdeath;
		stop;
	Death.Particles:
		TNT1 A 0 A_StartSound("cyberdemon/death",slot:CHAN_6);
		CBDI AABCAABCAA 5 A_SpawnItemEx("BD_CyberExplosion",frandom[bdsfx](-24,24),frandom[bdsfx](-24,24),frandom[bdsfx](32,104),flags:SXF_SETTARGET,failchance:48);
		TNT1 A 0 A_NoBlocking;
		CBDI BCAABC 5 A_SpawnItemEx("BD_CyberExplosion",frandom[bdsfx](-24,24),frandom[bdsfx](-24,24),frandom[bdsfx](32,104),flags:SXF_SETTARGET,failchance:36);
		TNT1 A 0 {
			if (bdoom_shakes)
				A_Quake(5,20,0,800,"");
			A_StartSound("cyberdemon/explosion",slot:CHAN_5,flags:CHANF_DEFAULT,attenuation:ATTN_NONE);
		}
		CBDD AAABBCBCCDDEEFFGG 1 BD_Bleed();
		TNT1 A 0 {
			A_FaceTarget();
			A_StartSound("gibs/gibbed");
			A_SpawnItemEx("BD_CyberHead",0,-8,80, frandom[bdsfx](-2,2),frandom[bdsfx](-2,2),frandom[bdsfx](5,9),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberHandLeft",0,-36,64, frandom[bdsfx](-1.5,1.5),frandom[bdsfx](-3.5,-5.5),frandom[bdsfx](4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberHandRight",0,36,64, frandom[bdsfx](-1.5,1.5),frandom[bdsfx](3.5,5.5),frandom[bdsfx](4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberLegLeft",0,-22,14, frandom[bdsfx](-1.5,1.5),frandom[bdsfx](-3.5,-5.5),frandom[bdsfx](2.5,4),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberLegRight",0,24,14, frandom[bdsfx](-1.5,1.5),frandom[bdsfx](3.5,5.5),frandom[bdsfx](2.5,4),flags:SXF_SETTARGET);
			
			A_SpawnItemEx("BD_CyberTorso1",0,36,64, frandom[bdsfx](-3,3),frandom[bdsfx](2,4),frandom[bdsfx](5,7),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberTorso2",0,-36,64, frandom[bdsfx](-3,3),frandom[bdsfx](-2,-4),frandom[bdsfx](5,7),flags:SXF_SETTARGET);
			A_SpawnItemEx("BD_CyberTorso3",0,0,26, frandom[bdsfx](-4,4),frandom[bdsfx](-4,4),frandom[bdsfx](3,6),flags:SXF_SETTARGET);
			
			for (int i = 10; i > 0; i--)
				A_SpawnItemEx("BD_BFountainSpray",frandom[bdsfx](-5.0,5.0),frandom[bdsfx](-5.0,5.0),random(24,72),frandom[bdsfx](-1.5,1.5),frandom[bdsfx](-1.5,1.5),frandom[bdsfx](4.5,6.5),0,SXF_SETTARGET,36);			
			for (int i = 6; i > 0; i--)
				A_SpawnItemEx("BD_BasicMeatPiece_Heavy",random(-4,4),random(-4,4),random(24,72),	frandom[bdsfx](-5,5),frandom[bdsfx](-5,5),frandom[bdsfx](5,8),	0,	SXF_SETTARGET, 64);
			for (int i = 6; i > 0; i--)
				A_SpawnItemEx("BD_BasicMetalPiece",frandom[bdsfx](-4,4),frandom[bdsfx](-4,4),frandom[bdsfx](24,72),		frandom[bdsfx](-10,10),frandom[bdsfx](-10,10),frandom[bdsfx](5,10),	0,SXF_SETTARGET, 64);
		}
		CBDO HHHIIIJJJ 1 {
			for (int i = 4; i > 0; i--)
				A_SpawnItemEx("BD_BFountainSpray",	frandom[bdsfx](-5.0,5.0),frandom[bdsfx](-5.0,5.0),random(24,72),frandom[bdsfx](-1.5,1.5),frandom[bdsfx](-1.5,1.5),frandom[bdsfx](4.5,6.5),0,SXF_SETTARGET,36);
			for (int i = 2; i > 0; i--)
				A_SpawnItemEx("BD_BasicMeatPiece_Heavy",frandom[bdsfx](-4,4),random(-4,4),random(24,72),	frandom[bdsfx](-5,5),frandom[bdsfx](-5,5),random(5,8),	0,	SXF_SETTARGET, 64);
			for (int i = 2; i > 0; i--)
				A_SpawnItemEx("BD_BasicMetalPiece",frandom[bdsfx](-4,4),frandom[bdsfx](-4,4),frandom[bdsfx](24,72),		frandom[bdsfx](-10,10),frandom[bdsfx](-10,10),frandom[bdsfx](5,10),	0,SXF_SETTARGET, 64);
		}
		CBDO K 3;
		CBDO L 150;
		CBDO L -1 A_BossDeath;
		stop;
	}
}

// Explosion that spawns blood as well as metal pieces â€” used by Cyberdemon, as well as Icon of Sin
Class BD_CyberExplosion : BD_BaseFlare {
	Default {
		+ROLLSPRITE
		renderstyle "Add";
		alpha 0.85;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		A_SetScale(frandom[bdsfx](0.65,1.0));
		roll = random(0,359);
		if (target && !(target is "BossBrain")) {
			A_StartSound("weapons/rlan/explosion",slot:CHAN_WEAPON,flags:CHANF_DEFAULT,1.0);
			A_Quake(2,8,0,512,"");
		}
		if(bdoom_blood) {
			for (int i = random(4,6); i > 0; i--)
				A_SpawnItemEx("BD_Blood",0,0,0,frandom[bdsfx](-3,3),frandom[bdsfx](-3,3),frandom[bdsfx](3,6),flags:SXF_SETTARGET);
		}
		if(bdoom_debris && target)
			A_SpawnItemEx("BD_BasicMetalPiece",random(-4,4),random(-4,4),random(10,48),		frandom[bdsfx](5,8),frandom[bdsfx](-2,2),random(4,7),	AngleTo(target)*random(170,190),SXF_SETTARGET, 144);	
	}
	states	{
	Spawn:
		BOM3 ABCDEFGHIJKLMNOPRTU 1 A_FadeOut(0.01);
		stop;
	}
}
