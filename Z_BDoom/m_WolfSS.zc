Class BD_WolfensteinSS : BDoomMonster  {
	Default	{
		species "WolfensteinSS";
		+DOHARMSPECIES
		Health 50;
		gibhealth 25;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 170;
		Monster;
		+FLOORCLIP
		SeeSound "wolfss/sight";
		PainSound "wolfss/pain";
		DeathSound "wolfss/death";
		ActiveSound "wolfss/active";
		AttackSound "";
		Obituary "$OB_WOLFSS";
		Tag "$FN_WOLFSS";
		Dropitem "Clip";
		BdoomMonster.monsterclip 32;
	}
	states	{
	Spawn:
		SSW1 E 10 A_Look();
		SSWR A 10 A_Look();
		loop;
	See:
		SSW1 ABCDDCBA 3 A_Chase();
		loop;
	Missile:
		TNT1 A 0 A_Jumpif(bdoom_weapons==2,"MissileModern");
		SSW1 EEFFF 2 A_FaceTarget();
		SSW1 F 10 A_FaceTarget();
		SSW1 G 4 BD_BulletAttack(22.5, 0, 1, random(1,5)*3, "wolfss/attack", "weapons/chaingun/fire","ChaingunCasing",10,-12,31,30,4);
		SSW1 F 6 A_FaceTarget();
		SSW1 G 4 BD_BulletAttack(22.5, 0, 1, random(1,5)*3, "wolfss/attack", "weapons/chaingun/fire","ChaingunCasing",10,-12,31,30,4);
		SSW1 F 6 A_FaceTarget();
		SSW1 F 1 A_CPosRefire();
		goto Missile+6;
	MissileModern:
		SSW1 EEFF 2 A_FaceTarget();
	MissileModernDo:
		SSW1 G 4 {
			if (bdoom_debris)
				A_SpawnItemEx("ChaingunCasing",14,-15,32,		frandom(2,4),0,random(2,4),	random(-85,-95),SXF_SETTARGET);
			A_StartSound("Mweapons/rifle/fire");
			A_CustomBulletAttack(20.0,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",30,4);
			monsterclip--;
		}
		SSW1 F 4 A_FaceTarget();
		SSW1 G 4 {
			if (bdoom_debris)
				A_SpawnItemEx("ChaingunCasing",14,-15,32,		frandom(2,4),0,random(2,4),	random(-85,-95),SXF_SETTARGET);
			A_StartSound("Mweapons/rifle/fire");
			A_CustomBulletAttack(20.0,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",30,4);
			monsterclip--;
		}
		SSW1 F 4 A_FaceTarget();
		TNT1 A 0 BD_Refire(8,"MissileModernEnd");
		goto MissileModernDo;
	MissileModernEnd:
		SSW1 FE 3;
		goto See;
	Reload:
		TNT1 A 0 {
			bFRIGHTENED = true;
			A_StartSound("Mweapons/rifle/clipout");
			monsterclip = 32;
		}
	    SSWV ABCDDCBA 3 A_Chase(null,null);
	    SSWV ABCDDCBA 3 A_Chase(null,null);
		TNT1 A 0 A_StartSound("Mweapons/rifle/clipin");
	    SSWV ABCDDCBA 3 A_Chase(null,null);
		TNT1 A 0 { bFRIGHTENED = false; }
		goto see;
	Pain:
		SSW1 H 3;
		SSW2 H 3 A_Pain();
		goto See;
	Death:
		SSWM AB 3 BD_Bleed();
		SSWM C 3 A_Scream();
		SSWM D 3;
		SSWM E 2 A_NoBlocking();
		SSWM FGH 2;
		SSWM IJK 3;
		goto deathbleed;
	XDeath:
		TNT1 A 0 A_JumpIf(bdoom_gibs,"XDeath.Particles");
	XDeath.Particles:
		TNT1 A 0 {
			A_XScream();
			A_NoBlocking();
			BD_Gibs();
			scale.x *= randompick[bdsfx](-1,1);
		}
		TR51 ABCDEFG 4;
		goto deathbleed;
	XDeath.Vanilla:
		SSWX BCD 2;
		SSWX E 3 A_XScream();
		SSWX FG 2;
		SSWX H 2 A_NoBlocking();
		SSWX IJKLMNOPQ 3;
		goto deathbleed;
	}
}
